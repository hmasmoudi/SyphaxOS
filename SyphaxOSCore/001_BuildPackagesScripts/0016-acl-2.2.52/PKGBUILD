# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=acl
pkgver=2.2.52
pkgrel=1
pkgdesc="acl"
arch=('x86_64')
url="http://download.savannah.gnu.org/releases/acl"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.src.tar.gz")
md5sums=('a61415312426e9c2212bd7dc7929abda')
depends=('rootfs>=1.0.0')

prepare() {
        cd "$srcdir/$pkgname-$pkgver"
        sed -i -e 's|/@pkg_name@|&-@pkg_version@|' include/builddefs.in
        sed -i "s:| sed.*::g" test/{sbits-restore,cp,misc}.test
        sed -i -e "/TABS-1;/a if (x > (TABS-1)) x = (TABS-1);" \
            libacl/__acl_to_any_text.c
}

build() {
        cd "$srcdir/$pkgname-$pkgver"
        ./configure --prefix=/usr    \
                    --disable-static \
                    --libexecdir=/usr/lib
                    
        make
}

package() {
        cd "$srcdir/$pkgname-$pkgver"

        make DESTDIR="$pkgdir/" install install-dev install-lib

        mkdir -pv "$pkgdir"/lib

        chmod -v 755 "$pkgdir"/usr/lib/libacl.so
        mv -v "$pkgdir"/usr/lib/libacl.so.* "$pkgdir"/lib
        ln -sfv ../../lib/$(readlink /usr/lib/libacl.so) "$pkgdir"/usr/lib/libacl.so
}
