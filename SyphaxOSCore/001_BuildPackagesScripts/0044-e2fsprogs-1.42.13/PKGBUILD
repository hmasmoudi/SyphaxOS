# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=e2fsprogs
pkgver=1.42.13
pkgrel=1
pkgdesc="The E2fsprogs package contains the utilities for handling the ext2, ext3 and ext4 file system."
arch=('x86_64')
url="http://downloads.sourceforge.net/project/e2fsprogs/e2fsprogs/v1.42.13"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.gz")
md5sums=('bc759fc62666786f5436e2075beb3265')
depends=('rootfs>=1.0.0')

prepare() {
        cd "$srcdir/$pkgname-$pkgver"
}

build() {
        cd "$srcdir/$pkgname-$pkgver"

        ./configure --prefix=/usr           \
                    --bindir=/bin           \
                    --with-root-prefix=""   \
                    --enable-elf-shlibs     \
                    --disable-libblkid      \
                    --disable-libuuid       \
                    --disable-uuidd         \
                    --disable-fsck
        
        make        
        makeinfo -o doc/com_err.info ./lib/et/com_err.texinfo
}

package() {
        cd "$srcdir/$pkgname-$pkgver"
        
        make DESTDIR="$pkgdir" install

        make DESTDIR="$pkgdir" install-libs

        chmod -v u+w $pkgdir/usr/lib/{libcom_err,libe2p,libext2fs,libss}.a
        gunzip -v $pkgdir/usr/share/info/libext2fs.info.gz
        install-info --dir-file=$pkgdir/usr/share/info/dir $pkgdir/usr/share/info/libext2fs.info
        install -v -m644 doc/com_err.info $pkgdir/usr/share/info
        install-info --dir-file=$pkgdir/usr/share/info/dir $pkgdir/usr/share/info/com_err.info

        rm -rf $pkgdir/bin/compile_et
        rm -rf $pkgdir/bin/mk_cmds
}
