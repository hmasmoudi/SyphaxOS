# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=xterm
pkgver=325
pkgrel=1
pkgdesc="xterm is a terminal emulator for the X Window System."
arch=('x86_64')
url="ftp://invisible-island.net/xterm"
install=$pkgname.install
license=('GPL')
groups=('core')
source=("$url/${pkgname}-${pkgver}.tgz")
md5sums=('e78a0bbc4d5fa3780917e447ae8ebc6b')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
   
   sed -i '/v0/{n;s/new:/new:kb=^?:/}' termcap
   printf '\tkbs=\\177,\n' >> terminfo
}

build() {
   cd "$srcdir/$pkgname-$pkgver"
   
   source /etc/profile.d/xorg.sh
   echo "Using XORG_PREFIX ($XORG_PREFIX) & XORG_CONFIG ($XORG_CONFIG)"

   TERMINFO=/usr/share/terminfo \
   ./configure $XORG_CONFIG     \
               --with-app-defaults=/etc/X11/app-defaults
   
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   
   #Make the package
   make DESTDIR="$pkgdir" install
}

