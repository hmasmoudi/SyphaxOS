# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=systemd
pkgver=230
pkgrel=1
pkgdesc="The systemd package contains programs for controlling the startup, running, and shutdown of the system."
arch=('x86_64')
install=$pkgname.install
url="http://anduin.linuxfromscratch.org/sources/other/systemd"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.xz")
md5sums=('7fd9c7b1296104ccc0b8aee4be3d3c23')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   autoreconf
   sed -e 's:test/udev-test.pl ::g'  \
       -e 's:test-copy$(EXEEXT) ::g' \
       -i Makefile.in
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   cc_cv_CFLAGS__flto=no                \
   XSLTPROC="/usr/bin/xsltproc"         \
   ./configure --prefix=/usr            \
               --sysconfdir=/etc        \
               --localstatedir=/var     \
               --with-rootprefix=       \
               --with-rootlibdir=/lib   \
               --enable-split-usr       \
               --disable-firstboot      \
               --disable-ldconfig       \
               --disable-sysusers       \
               --without-python         \
               --with-default-dnssec=no \
               --docdir=/usr/share/doc/systemd-$pkgver

   make LIBRARY_PATH=/lib
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   make LD_LIBRARY_PATH=/lib DESTDIR="$pkgdir" install

   mkdir -pv $pkgdir/lib
   mkdir -pv $pkgdir/sbin

   mv -v $pkgdir/usr/lib/libnss_{myhostname,mymachines,resolve}.so.2 $pkgdir/lib
   rm -rfv $pkgdir/usr/lib/rpm
   for tool in runlevel reboot shutdown poweroff halt telinit; do
      ln -sfv ../bin/systemctl $pkgdir/sbin/${tool}
   done
   ln -sfv ../lib/systemd/systemd $pkgdir/sbin/init

   cd $pkgdir/etc && ln -snf ../run/systemd/resolve/resolv.conf
}
