# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=shadow
pkgver=4.2.1
pkgrel=1
pkgdesc="The Shadow package contains programs for handling passwords in a secure way."
arch=('x86_64')
url="http://pkg-shadow.alioth.debian.org/releases"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.xz")
md5sums=('2bfafe7d4962682d31b5eba65dba4fc8')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   sed -i 's/groups$(EXEEXT) //' src/Makefile.in
   find man -name Makefile.in -exec sed -i 's/groups\.1 / /'   {} \;
   find man -name Makefile.in -exec sed -i 's/getspnam\.3 / /' {} \;
   find man -name Makefile.in -exec sed -i 's/passwd\.5 / /'   {} \;
   sed -i -e 's@#ENCRYPT_METHOD DES@ENCRYPT_METHOD SHA512@' \
          -e 's@/var/spool/mail@/var/mail@' etc/login.defs
   sed -i 's/1000/999/' etc/useradd
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   ./configure --sysconfdir=/etc --with-group-name-max-length=32

   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   make DESTDIR="$pkgdir" install

   mv -v $pkgdir/usr/bin/passwd $pkgdir/bin

   #FIXME To be checked
   #pwconv
   #grpconv

   sed -i 's/yes/no/' $pkgdir/etc/default/useradd

   install -v -m644 $pkgdir/etc/login.defs $pkgdir/etc/login.defs.orig &&
   for FUNCTION in FAIL_DELAY               \
                   FAILLOG_ENAB             \
                   LASTLOG_ENAB             \
                   MAIL_CHECK_ENAB          \
                   OBSCURE_CHECKS_ENAB      \
                   PORTTIME_CHECKS_ENAB     \
                   QUOTAS_ENAB              \
                   CONSOLE MOTD_FILE        \
                   FTMP_FILE NOLOGINS_FILE  \
                   ENV_HZ PASS_MIN_LEN      \
                   SU_WHEEL_ONLY            \
                   CRACKLIB_DICTPATH        \
                   PASS_CHANGE_TRIES        \
                   PASS_ALWAYS_WARN         \
                   CHFN_AUTH ENCRYPT_METHOD \
                   ENVIRON_FILE
   do
       sed -i "s/^${FUNCTION}/# &/" $pkgdir/etc/login.defs
   done

   #Copy configuration file
   cp ../../login $pkgdir/etc/pam.d/login
   cp ../../passwd $pkgdir/etc/pam.d/passwd
   cp ../../su $pkgdir/etc/pam.d/su
   cp ../../chage $pkgdir/etc/pam.d/chage

   for PROGRAM in chfn chgpasswd chpasswd chsh groupadd groupdel \
                  groupmems groupmod newusers useradd userdel usermod
   do
       install -v -m644 $pkgdir/etc/pam.d/chage $pkgdir/etc/pam.d/${PROGRAM}
       sed -i "s/chage/$PROGRAM/" $pkgdir/etc/pam.d/${PROGRAM}
   done
   [ -f $pkgdir/etc/login.access ] && mv -v $pkgdir/etc/login.access{,.NOUSE}
   [ -f $pkgdir/etc/limits ] && mv -v $pkgdir/etc/limits{,.NOUSE}
}
