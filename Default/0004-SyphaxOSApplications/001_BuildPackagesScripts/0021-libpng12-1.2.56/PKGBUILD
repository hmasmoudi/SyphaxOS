# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=libpng12
pkgver=1.2.56
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="https://sourceforge.net/projects/libpng/files"
license=('GPL')
groups=('applications')
source=("$url/libpng-${pkgver}.tar.xz"
        "libpng-1.2.56-apng.patch")
md5sums=('868562bd1c58b76ed8703f135a2e439a'
         '5ba30446f3c9d494b6ca50bee438a1d7')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/libpng-${pkgver}"
   
   patch -Np0 -i ../libpng-${pkgver}-apng.patch

   libtoolize --force --copy
   aclocal
   autoconf
   automake --add-missing
}

build() {
   cd "$srcdir/libpng-${pkgver}"
   
   ./configure --prefix=/usr
   
   make
}

package() {
   cd "$srcdir/libpng-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
   rm -rf "${pkgdir}"/usr/{bin,include,lib/{libpng.{a,so,la},pkgconfig},share}
}

