# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=p7zip
pkgver=16.02
pkgrel=2
pkgdesc="Command-line file archiver with high compression ratio."
arch=('x86_64')
url="https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver"
license=('GPL')
groups=('applications')
source=("$url/${pkgname}_${pkgver}_src_all.tar.bz2")
md5sums=('a0128d661cfe7cc8c121e73519c54fbf')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname}_$pkgver"

   if [[ $CARCH = x86_64 ]]; then
      cp makefile.linux_amd64_asm makefile.machine
   else
      cp makefile.linux_x86_asm_gcc_4.X makefile.machine
   fi
}

build() {
   cd "$srcdir/${pkgname}_${pkgver}"
   
   make all3 OPTFLAGS="$CFLAGS"
}

package() {
   cd "$srcdir/${pkgname}_${pkgver}"
   
   make install \
        DEST_DIR="$pkgdir" \
        DEST_HOME=/usr \
        DEST_MAN=/usr/share/man

   # Remove documentation for the GUI file manager
   rm -r "$pkgdir/usr/share/doc/p7zip/DOC/MANUAL/fm"

   install -d "${pkgdir}"/usr/share/licenses/p7zip
   ln -s -t "$pkgdir/usr/share/licenses/p7zip/" \
            /usr/share/doc/p7zip/DOC/License.txt \
            /usr/share/doc/p7zip/DOC/unRarLicense.txt
}

