# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=gnupg
pkgver=2.1.20
pkgrel=5
pkgdesc="The GnuPG package is GNU's tool for secure communication and data storage."
arch=('x86_64')
url="ftp://ftp.gnupg.org/gcrypt/gnupg"
license=('GPL')
install=${pkgname}.install
groups=('applications')
source=("$url/${pkgname}-${pkgver}.tar.bz2")
md5sums=('SKIP')
depends=('rootfs>=2.0.0')

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr \
               --enable-symcryptrun   \
               --docdir=/usr/share/doc/gnupg-${pkgver}
   make
   
   makeinfo --html --no-split \
            -o doc/gnupg_nochunks.html doc/gnupg.texi
   makeinfo --plaintext       \
            -o doc/gnupg.txt           doc/gnupg.texi
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
   
   install -v -m755 -d ${pkgdir}/usr/share/doc/gnupg-${pkgver}/html
   install -v -m644    doc/gnupg_nochunks.html \
                       ${pkgdir}/usr/share/doc/gnupg-${pkgver}/html/gnupg.html
   install -v -m644    doc/*.texi doc/gnupg.txt \
                       ${pkgdir}/usr/share/doc/gnupg-${pkgver}
   for f in gpg gpgv
   do
     ln -svf ${f}2.1 ${pkgdir}/usr/share/man/man1/$f.1 &&
     ln -svf ${f}2   ${pkgdir}/usr/bin/$f
   done
   unset f
}

