# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=evince
pkgver=3.26.0
pkgrel=4
pkgdesc="Evince is a document viewer for multiple document formats. It supports PDF, Postscript, DjVu, TIFF and DVI."
arch=('x86_64')
url="http://ftp.gnome.org/pub/gnome/sources/evince/3.26"
license=('GPL')
groups=('applications')
source=("$url/${pkgname}-${pkgver}.tar.xz")
sha256sums=('79567bdb743cf0c3ed7b638da32afc9b850298f9b4edd532455df4a7e2a4c9d8')
depends=('rootfs' 'poppler')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -i "/seems to be moved/s/^/#/" ltmain.sh
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr                     \
               --enable-compile-warnings=minimum \
               --enable-introspection            \
               --disable-static 

   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
}

