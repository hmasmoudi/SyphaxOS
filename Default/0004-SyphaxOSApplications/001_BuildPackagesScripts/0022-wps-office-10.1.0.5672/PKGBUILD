# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=wps-office
pkgver=10.1.0.5707
pkgrel=2
pkgdesc="WPS Office 2016 Free The perfect free office software. Compatible avec Microsoft Office."
arch=('x86_64')
url="http://kdl1.cache.wps.com/ksodl/download/linux/a21"
license=('GPL')
install=${pkgname}.install
groups=('applications')
source=("$url/${pkgname}_${pkgver}~a21_x86_64.tar.xz")
sha1sums=('1970df8c0e6a03649b6809472b89628a685188dc')
depends=('rootfs>=2.0.0'
         'libpng12'
         'xdg-utils')

prepare() {
   cd ${pkgname}_${pkgver}~a21_${arch}

   sed -i 's|/opt/kingsoft/wps-office|/usr/lib|' wps wpp et
}

package() {
   cd ${pkgname}_${pkgver}~a21_${arch}

   install -d "$pkgdir/usr/lib"
   cp -r office6 "$pkgdir/usr/lib"

   install -d "$pkgdir/usr/bin"
   install -m755 wps wpp et "$pkgdir/usr/bin"

   install -d "$pkgdir/usr/share/applications"
   cp -r resource/applications/* "$pkgdir/usr/share/applications"

   install -d "$pkgdir/usr/share/icons"
   cp -r resource/icons/* "$pkgdir/usr/share/icons"

   install -d "$pkgdir/usr/share/mime"
   cp -r resource/mime/* "$pkgdir/usr/share/mime"

   #cp -r "$srcdir/usr/share" "$pkgdir/usr/"

   install -d "$pkgdir/usr/share/fonts/wps-office"
   cp -r fonts/* "$pkgdir/usr/share/fonts/wps-office"

   install -Dm644 office6/mui/default/EULA.txt "$pkgdir/usr/share/licenses/$pkgname/EULA.txt"
}

