# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=gst-libav
pkgver=1.14.1
pkgrel=2
pkgdesc="The GStreamer Libav package contains GStreamer plugins for Libav (a fork of FFmpeg)."
arch=('x86_64')
url="http://gstreamer.freedesktop.org/src/gst-libav"
license=('GPL')
groups=('applications')
source=("$url/${pkgname}-${pkgver}.tar.xz")
sha256sums=('eff80a02d2f2fb9f34b67e9a26e9954d3218c7aa18e863f2a47805fa7066029d')
depends=('rootfs')

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
               --with-package-name="GStreamer Libav Plugins SyphaxOS" \
               --with-package-origin="http://gstreamer.freedesktop.org/src/gst-libav" \
               --enable-orc  \
               --disable-static

   # https://bugzilla.gnome.org/show_bug.cgi?id=655517
   sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
   
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
}

