# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=flatpak
pkgver=0.11.8.3
pkgrel=2
pkgdesc="THE FUTURE OF APPLICATION DISTRIBUTION. Flatpak is the new framework for desktop applications on Linux."
arch=('x86_64')
url="https://github.com/flatpak/flatpak/releases/download/${pkgver}"
license=('GPL')
groups=('applications')
source=("$url/${pkgname}-${pkgver}.tar.xz"
        "flatpak-bindir.sh"
        "flathub.flatpakrepo"
        "flathub.desktop")
sha256sums=('SKIP'
            'cf20e4831bceaaee74e9298b4ad7c4d2d51085d43f62d1c6a2d2977d2f421e00'
            '3371dd250e61d9e1633630073fefda153cd4426f72f4afa0c3373ae2e8fea03a'
            'e338b7d26e30e7400abff52a587cf14e38ec93807780b490a41419af98f96952')
depends=('rootfs' 'ostree' 'elfutils' 'bubblewrap')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -i "/seems to be moved/s/^/#/" ltmain.sh
}
build() {
   cd "$srcdir/${pkgname}-${pkgver}"
      
   ./configure \
       --prefix=/usr \
       --sysconfdir=/etc \
       --localstatedir=/var \
       --sbindir=/usr/bin \
       --libexecdir=/usr/lib/$pkgname \
       --disable-static \
       --with-system-bubblewrap \
       --with-priv-mode=setuid \
       --enable-system-helper \
       --with-dbus-config-dir=/usr/share/dbus-1/system.d

   sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
   
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
   
  install -Dt "$pkgdir/etc/profile.d" -m644 ../flatpak-bindir.sh
  install -Dt "$pkgdir/usr/share/flatpak" -m644 ../flathub.flatpakrepo
  install -Dt "$pkgdir/usr/share/applications" -m644 ../flathub.desktop
  
  # Fixup mode to match polkit
  install -d -o root -g 102 -m 700 "$pkgdir/usr/share/polkit-1/rules.d"
}

