# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgbase=openal
pkgname=(openal)
pkgver=1.17.2
pkgrel=2
pkgdesc="Cross-platform 3D audio library, software implementation"
arch=(i686 x86_64)
url="https://github.com/kcat/openal-soft"
license=(LGPL)
depends=(glibc)
source=("git+$url#tag=openal-soft-$pkgver")
sha256sums=('SKIP')

prepare() {
  mkdir build
  cd $pkgbase-soft
  sed -i 's/PIX_FMT_YUV420P/AV_&/g' examples/alffplay.c
}

build() {
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release ../$pkgbase-soft
  make
}

package_openal() {
  optdepends=('qt4: alsoft-config GUI Configurator'
              'fluidsynth: MIDI rendering')

  make -C build DESTDIR="$pkgdir/" install
  install -m644 $pkgbase-soft/{env-vars,hrtf}.txt "$pkgdir/usr/share/openal/"
}

