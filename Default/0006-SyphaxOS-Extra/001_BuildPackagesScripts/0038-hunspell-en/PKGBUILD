# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hunspell-en
pkgname=('hunspell-en_US' 'hunspell-en_GB'  'hunspell-en')
pkgver=2017.08.24
pkgrel=2
pkgdesc="English hunspell dictionaries"
arch=(x86_64)
url="http://wordlist.aspell.net/dicts/"
license=('LGPL' 'custom:scowl')
optdepends=('hunspell: the spell checking libraries and apps')
source=(http://downloads.sourceforge.net/project/wordlist/speller/$pkgver/hunspell-en_US-$pkgver.zip
        http://downloads.sourceforge.net/project/wordlist/speller/$pkgver/hunspell-en_GB-large-$pkgver.zip)
sha1sums=('SKIP'
          'SKIP')

prepare() {
  sed -i "s:SET UTF8:SET UTF-8:g" *.aff
}

package_hunspell-en_US() {
  pkgdesc="US English hunspell dictionaries"

  cd "$srcdir"
  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 en_US*.dic en_US*.aff $pkgdir/usr/share/hunspell

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

  # licenses
  install -D -m644 README_en_US.txt $pkgdir/usr/share/licenses/$pkgname/Copyright_en_US
}

package_hunspell-en_GB() {
  pkgdesc="GB English hunspell dictionaries"

  cd "$srcdir"
  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 en_GB*.dic en_GB*.aff $pkgdir/usr/share/hunspell

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

  # licenses
  install -D -m644 README_en_GB-large.txt $pkgdir/usr/share/licenses/$pkgname/Copyright_en_GB-large
}

package_hunspell-en() {
  pkgdesc="English (US, UK, etc.) hunspell dictionaries"
  depends=('hunspell-en_US' 'hunspell-en_GB')

  cd "$srcdir"
  install -dm755 ${pkgdir}/usr/share/hunspell

  pushd $pkgdir/usr/share/hunspell/
    en_GB_aliases="en_AG en_BS en_BW en_BZ en_DK en_GB en_GH en_HK en_IE en_IN en_JM en_NA en_NG en_NZ en_SG en_TT en_ZA en_ZW"
    for lang in $en_GB_aliases; do
      ln -s en_GB-large.aff $lang.aff
      ln -s en_GB-large.dic $lang.dic
    done
    en_US_aliases="en_PH"
    for lang in $en_US_aliases; do
      ln -s en_US.aff $lang.aff
      ln -s en_US.dic $lang.dic
    done
  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
}
