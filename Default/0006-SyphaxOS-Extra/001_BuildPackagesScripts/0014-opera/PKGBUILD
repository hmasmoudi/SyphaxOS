# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=opera
pkgver=44.0.2510.857
pkgrel=1
pkgdesc="A fast and secure web browser"
install=${pkgname}.install
arch=('x86_64')
url="http://get.geo.opera.com/pub/${pkgname}/desktop/${pkgver}/linux"
license=('GPL')
groups=('applications')
source=("$url/${pkgname}-stable_${pkgver}_amd64.deb"
        "opera"
        "default")
md5sums=('2c7dc68ebea5c7f65d711af0d82dbac1'
         'c8150bcdead9465aa9b815062eecbbdc'
         'df0f13b3985dd9e0be360a7a496cd142')
depends=('rootfs>=2.0.0'
         'pepper-flash>=25.0.0.127')

prepare() {
    sed -e "s/%pkgname%/$pkgname/g" -i "$srcdir/opera"
    sed -e "s/%operabin%/$pkgname\/$pkgname/g" \
        -i "$srcdir/opera"
}


package() {
    tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"

    # get rid of the extra subfolder {i386,x86_64}-linux-gnu
    (
        cd "$pkgdir/usr/lib/"*-linux-gnu/
        mv "$pkgname" ../
    )
    rm -rf "$pkgdir/usr/lib/"*-linux-gnu

    # suid opera_sandbox
    chmod 4755 "$pkgdir/usr/lib/$pkgname/opera_sandbox"

    # install default options
    install -Dm644 "$srcdir/default" "$pkgdir/etc/$pkgname/default"

    # install opera wrapper
    rm "$pkgdir/usr/bin/$pkgname"
    install -Dm755 "$srcdir/opera" "$pkgdir/usr/bin/$pkgname"

    # license
    install -Dm644 \
        "$pkgdir/usr/share/doc/${pkgname}-stable/copyright" \
        "$pkgdir/usr/share/licenses/$pkgname/copyright"
}

