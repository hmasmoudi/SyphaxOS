# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=meson
pkgver=0.45.0
pkgrel=2
pkgdesc='High productivity build system'
url='http://mesonbuild.com/'
arch=('x86_64')
license=('Apache')
depends=('ninja')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz)
sha256sums=('SKIP')

package() {
  cd ${pkgname}-${pkgver}
  
  python3 setup.py install --root="${pkgdir}" --optimize=1
  
  for _f in data/syntax-highlighting/vim/*/*; do
    install -Dt "${pkgdir}/usr/share/vim/vimfiles/$(basename "$(dirname "$_f")")" -m644 "$_f"
  done
  install -Dt "${pkgdir}/usr/share/emacs/site-lisp" -m644 data/syntax-highlighting/emacs/*
  install -Dt "${pkgdir}/usr/share/zsh/site-functions" -m644 data/shell-completions/zsh/*
}

