# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>
# Contributors: Refer to https://aur.archlinux.org/packages/jre/
pkgname=jre
_major=8
_minor=112
_build=b15
_jname=${pkgname}${_major}
_jvmdir=/usr/lib/jvm/java-$_major-$pkgname/jre
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -b oraclelicense=a -o %o %u')
pkgver=${_major}u${_minor}
pkgrel=3
pkgdesc="Oracle Java Runtime Environment"
arch=('x86_64')
install=${pkgname}.install
url="http://download.oracle.com/otn-pub/java"
license=('GPL')
groups=('applications')
source=("$url/jdk/$pkgver-$_build/jre-${_major}u${_minor}-linux-x64.tar.gz"
        "$url/jce/$_major/jce_policy-$_major.zip"
        "policytool-$_jname.desktop")
md5sums=('5ccc09b2cbbf715b583fad72b070b69d'
         'b3c7031bc65c28c2340302065e7d00d3'
         '762729fa0faba8ff2b5a29c249c95079')
depends=('rootfs>=2.0.0')

backup=("etc/java-$_jname/amd64/jvm.cfg"
        "etc/java-$_jname/images/cursors/cursors.properties"
        "etc/java-$_jname/management/jmxremote.access"
        "etc/java-$_jname/management/management.properties"
        "etc/java-$_jname/security/java.policy"
        "etc/java-$_jname/security/java.security"
        "etc/java-$_jname/security/javaws.policy"
        "etc/java-$_jname/content-types.properties"
        "etc/java-$_jname/flavormap.properties"
        "etc/java-$_jname/fontconfig.properties.src"
        "etc/java-$_jname/logging.properties"
        "etc/java-$_jname/net.properties"
        "etc/java-$_jname/psfont.properties.ja"
        "etc/java-$_jname/psfontj2d.properties"
        "etc/java-$_jname/sound.properties")
        
package() {
   cd ${pkgname}1.${_major}.0_${_minor}

   echo "Creating directory structure..."
   install -d "$pkgdir"/etc/.java/.systemPrefs
   install -d "$pkgdir"/usr/lib/jvm/java-$_major-$pkgname/jre/bin
   install -d "$pkgdir"/usr/lib/mozilla/plugins
   install -d "$pkgdir"/usr/share/licenses/java$_major-$pkgname

   echo "Removing redundancies..."
   rm -r lib/desktop/icons/HighContrast/
   rm -r lib/desktop/icons/HighContrastInverse/
   rm -r lib/desktop/icons/LowContrast/
   rm    lib/fontconfig.*.bfc
   rm    lib/fontconfig.*.properties.src
   rm    man/ja
   rm -r plugin/

   echo "Moving contents..."
   mv * "$pkgdir"/$_jvmdir

   # Cd to the new playground
   cd "$pkgdir"/$_jvmdir

   echo "Fixing directory structure..."
   # Suffix .desktops + icons (sun-java.png -> sun-java-$_jname.png)
   for i in $(find lib/desktop/ -type f); do
     rename -- "." "-$_jname." $i
   done

   # Fix .desktop paths
   sed -e "s|Exec=|Exec=$_jvmdir/bin/|" \
     -e "s|.png|-$_jname.png|" \
   -i lib/desktop/applications/*

   # Move .desktops + icons to /usr/share
   mv lib/desktop/* "$pkgdir"/usr/share/
   install -m644 "$srcdir"/*.desktop "$pkgdir"/usr/share/applications/

   # Move confs to /etc and link back to /usr: /usr/lib/jvm/java-$_jname/lib -> /etc
   for new_etc_path in ${backup[@]}; do
     # Old location
     old_usr_path="lib/${new_etc_path#*$_jname/}"

     # Move
     install -Dm644 "$old_usr_path" "$pkgdir/$new_etc_path"
     ln -sf "/$new_etc_path" "$old_usr_path"
   done

   # Link NPAPI plugin
   ln -sf $_jvmdir/lib/amd64/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/libnpjp2-$_jname.so

   # Create default Java link
   mkdir -p "$pkgdir"/usr/bin
   pushd "$pkgdir"/usr/lib/jvm/java-8-jre/jre/bin/
   for JavaBin in *; do
     ln -sf /usr/lib/jvm/java-8-jre/jre/bin/$JavaBin "$pkgdir"/usr/bin/$JavaBin
   done
   popd

   # Replace JKS keystore with 'ca-certificates-java'
   ln -sf /etc/ssl/certs/java/cacerts lib/security/cacerts

   # Suffix man pages
   for i in $(find man/ -type f); do
     mv "$i" "${i/.1}-$_jname.1"
   done

   # Move man pages
   mv man/ja_JP.UTF-8/ man/ja
   mv man/ "$pkgdir"/usr/share

   # Move/link licenses
   mv COPYRIGHT LICENSE README *.txt "$pkgdir"/usr/share/licenses/java$_major-$pkgname/
   ln -sf /usr/share/licenses/java$_major-$pkgname/ "$pkgdir"/usr/share/licenses/$pkgname

   echo "Installing Java Cryptography Extension (JCE) Unlimited Strength Jurisdiction Policy Files..."
   # Replace default "strong", but limited, cryptography to get an "unlimited strength" one for
   # things like 256-bit AES. Enabled by default in OpenJDK:
   # - http://suhothayan.blogspot.com/2012/05/how-to-install-java-cryptography.html
   # - http://www.eyrie.org/~eagle/notes/debian/jce-policy.html
   install -m644 "$srcdir"/UnlimitedJCEPolicyJDK$_major/*.jar lib/security/
   install -Dm644 "$srcdir"/UnlimitedJCEPolicyJDK$_major/README.txt \
                "$pkgdir"/usr/share/doc/$pkgname/README_-_Java_JCE_Unlimited_Strength.txt

   echo "Enabling copy+paste in unsigned applets..."
   # Copy/paste from system clipboard to unsigned Java applets has been disabled since 6u24:
   # - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java
   # - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html
   _line=$(awk '/permission/{a=NR}; END{print a}' "$pkgdir"/etc/java-$_jname/security/java.policy)
   sed "$_line a\\\\n \
     // (AUR) Allow unsigned applets to read system clipboard, see:\n \
     // - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java\n \
     // - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html\n \
     permission java.awt.AWTPermission \"accessClipboard\";" \
      -i "$pkgdir"/etc/java-$_jname/security/java.policy
}

