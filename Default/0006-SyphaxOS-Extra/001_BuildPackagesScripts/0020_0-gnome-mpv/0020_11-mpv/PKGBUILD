# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=mpv
pkgver=0.27.2
pkgrel=6
_waf_version=1.9.15
pkgdesc='a free, open source, and cross-platform media player'
arch=('x86_64')
# We link against libraries that are licensed GPLv3 explicitly, libsmbclient
# being one of these. So our package is GPLv3 only as well.
license=('GPL3')
url='https://mpv.io/'
depends=('rootfs'
         'libvdpau-va-gl'
         'clutter-gst'
         'glu'
         'v4l-utils'
         'libvpx'
         'orc'
         'fribidi'
         'libass'
         'xvidcore'
         'x265'
         'libx264'
         'ffmpeg')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz"
        "0001-opengl-backend-support-multiple-backends.patch"
        "0002-vaapi-Use-libva2-message-callbacks.patch"
        "0003-demux_lavf-return-AVERROR_EOF-on-file-end.patch"
        "https://waf.io/waf-${_waf_version}")
sha256sums=('SKIP'
            '609e0530f1b0cdb910dcffb5f62bf55936540e24105ce1b2daf1bd6291a7d58a'
            '3c3517f4f4c71e39e1e04ea440688fc8d7b3dc55e6bc0a9398d11a9b75bde07d'
            '5de6c616428c87cf9b39d8ba24446d65d175050c083e1054194d93cf03d5816a'
            'SKIP')

prepare() {
  cd ${pkgname}-${pkgver}

  # --opengl-backend: support multiple backends (#4384) (FS#53962)
  patch -Np1 < "${srcdir}"/0001-opengl-backend-support-multiple-backends.patch

  # vaapi: Use libva2 message callbacks
  patch -Np1 < "${srcdir}"/0002-vaapi-Use-libva2-message-callbacks.patch

  # demux_lavf: return AVERROR_EOF on file end
  patch -Np1 < "${srcdir}"/0003-demux_lavf-return-AVERROR_EOF-on-file-end.patch
  install -m755 "${srcdir}"/waf-${_waf_version} waf
}

build() {
  cd ${pkgname}-${pkgver}

  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-libass \
    --enable-dvb \
    --enable-encoding \
    --enable-libarchive \
    --enable-libmpv-shared \
    --enable-libsmbclient \
    --enable-zsh-comp \
    --enable-vdpau-hwaccel \
    --enable-vaapi-hwaccel

  ./waf build
}

package() {
  cd ${pkgname}-${pkgver}

  ./waf install --destdir="$pkgdir"

  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
