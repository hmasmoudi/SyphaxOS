# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=evolution-ews
pkgver=3.22.5
pkgrel=1
pkgdesc="MS Exchange integration through Exchange Web Services"
url="https://wiki.gnome.org/Apps/Evolution/EWS"
arch=(i686 x86_64)
license=(LGPL2.1)
depends=(libmspack evolution)
options=(!emptydirs)
source=("http://ftp.gnome.org/pub/gnome/sources/evolution-ews/3.22/evolution-ews-3.22.5.tar.xz")
sha256sums=('de7c7651972374ce1dc4f7b8a876ed91e3317968b063340033edbdfb3d275ab0')

prepare() {
  cd $pkgname-$pkgver
  
  sed -i "/seems to be moved/s/^/#/" ltmain.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
