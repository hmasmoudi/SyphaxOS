# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=subversion
pkgver=1.9.7
pkgrel=2
pkgdesc="Subversion is a version control system that is designed to be a compelling replacement for CVS in the open source community."
arch=('x86_64')
url="https://www.apache.org/dist/subversion"
license=('APACHE')
groups=('applications')
source=("$url/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('874b81749cdc3e88152d103243c3623ac6338388')
depends=('rootfs' 'apr' 'apr-util' 'serf')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -i "/seems to be moved/s/^/#/" build/ltmain.sh
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr    \
               --disable-static \
               --with-apache-libexecdir &&
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install

   install -v -m755 -d "${pkgdir}"/usr/share/doc/subversion-${pkgver} &&
   cp      -v -R       doc/* \
                       "${pkgdir}"/usr/share/doc/subversion-${pkgver}
}

