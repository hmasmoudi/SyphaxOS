# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=vivaldi
pkgver=1.7.735.46
pkgrel=1
pkgdesc='An advanced browser made with the power user in mind.'
url="https://vivaldi.com"
install=${pkgname}.install
license=('custom')
arch=('x86_64')
depends=('rootfs>=2.0.0')
source=("https://downloads.vivaldi.com/stable/vivaldi-stable_${pkgver}-1_amd64.deb")
sha256sums=('bb15313f9d7f17bc135b5a41df0aaf83b1bdc1867bf3fe4fce5757a0bbadebbe')

package() {
    tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"

    # suid sandbox
    chmod 4755 "$pkgdir/opt/vivaldi/vivaldi-sandbox"

    binf="$pkgdir/usr/bin/vivaldi-stable"
    if [[ ! -e "$binf" ]] && [[ ! -f "$binf" ]] && [[ ! -L "$binf" ]]; then
        install -dm755 "$pkgdir/usr/bin"
        ln -s /opt/vivaldi/vivaldi "$binf"
    fi

    # install icons
    for res in 16 22 24 32 48 64 128 256; do
        install -Dm644 "$pkgdir/opt/vivaldi/product_logo_${res}.png" \
            "$pkgdir/usr/share/icons/hicolor/${res}x${res}/apps/vivaldi.png"
    done
}

