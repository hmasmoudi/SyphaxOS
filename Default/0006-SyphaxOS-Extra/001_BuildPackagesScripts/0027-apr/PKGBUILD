# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=apr
pkgver=1.6.2
pkgrel=2
pkgdesc="The Apache Portable Runtime."
arch=('x86_64')
url="https://archive.apache.org/dist/apr"
license=('APACHE')
groups=('applications')
source=("$url/${pkgname}-${pkgver}.tar.bz2")
md5sums=('e81a851967c79b5ce9bfbc909e4bf735')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   touch libtoolT
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr    \
               --disable-static \
               --with-installbuilddir=/usr/share/apr-1/build
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
}

