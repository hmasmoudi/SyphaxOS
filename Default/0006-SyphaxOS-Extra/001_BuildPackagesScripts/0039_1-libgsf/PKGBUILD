# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=libgsf
pkgver=1.14.41+3+gc7b4b53
pkgrel=2
arch=('x86_64')
pkgdesc="An extensible I/O abstraction library for dealing with structured file formats"
url="https://git.gnome.org/browse/libgsf"
license=('GPL' 'LGPL')
depends=('libxml2' 'gdk-pixbuf' 'bzip2')
_commit=c7b4b53020d507dccf8ba73c5a2a3a23cc649562  # master
source=("git+https://git.gnome.org/browse/libgsf#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^LIBGSF_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-introspection
  make
}

package_libgsf() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}
