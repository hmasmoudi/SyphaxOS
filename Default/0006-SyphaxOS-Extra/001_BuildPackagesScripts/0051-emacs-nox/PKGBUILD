# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=emacs-nox
pkgver=25.1
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting real-time display editor, without X11 support"
arch=('x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'libxml2' 'glib' 'gnutls')
provides=('emacs')
conflicts=('emacs')
changelog=$pkgname.changelog
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-$pkgver.tar.xz)
validpgpkeys=('28D3BED851FDF3AB57FEF93C233587A47C207910')
sha256sums=('19f2798ee3bc26c95dca3303e7ab141e7ad65d6ea2b6945eeba4dbea7df48f33')

build() {
  cd "${srcdir}"/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-x --without-sound
  make
}

package() {
  cd "${srcdir}"/emacs-$pkgver

  make DESTDIR="${pkgdir}" install

# remove conflict with ctags package
  mv "${pkgdir}"/usr/bin/{ctags,ctags.emacs}
  mv "${pkgdir}"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

# fix user/root permissions on usr/share files
  find "${pkgdir}"/usr/share/emacs/$pkgver -exec chown root.root {} \;
# remove .desktop file and icons
  rm -rf "${pkgdir}"/usr/share/{applications,icons}
# fix perms on /var/games
  rm -rf "${pkgdir}"/var/games
}
