# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=hunspell
pkgver=1.6.2
pkgrel=1
pkgdesc="Spell checker and morphological analyzer library and program"
arch=('x86_64')
url="http://hunspell.github.io/"
license=('GPL' 'LGPL' 'MPL')
depends=('rootfs')
source=($pkgname-$pkgver.tar.gz::https://github.com/hunspell/hunspell/archive/v${pkgver}.tar.gz)
sha256sums=('SKIP')

prepare() {
  cd hunspell-$pkgver
  autoreconf -vfi
}

build() {
  cd hunspell-$pkgver
  ./configure --prefix=/usr --disable-static \
	--with-ui --with-readline # --with-experimental breaks build in this release
  make
}

check() {
  cd hunspell-$pkgver
  make check
}

package() {
  cd hunspell-$pkgver
  make DESTDIR="$pkgdir" install
  
  # add generic hunspell.so for development and projects not using pkgconfig flags - FS#30592
  pushd $pkgdir/usr/lib
  ln -s libhunspell-?.?.so libhunspell.so
  popd
}
