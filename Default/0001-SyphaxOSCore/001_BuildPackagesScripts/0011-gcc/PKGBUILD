# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=gcc
pkgver=7.2.0
pkgrel=20170827
_commit=45afd2567a11afd218b175a78321f6412ba7f2f5
pkgdesc="The GCC package contains the GNU compiler collection, which includes the C and C++ compilers."
arch=('x86_64')
url="http://gcc.gnu.org"
license=('GPL')
groups=('core')
options=('!emptydirs')
source=("gcc-${pkgver}-${pkgrel}.tgz::https://gcc.gnu.org/git/?p=gcc.git;a=snapshot;h=${_commit};sf=tgz")
md5sums=('SKIP')
depends=('rootfs>=2.0.0'
         'binutils>=2.28'
         'mpc')

prepare() {
   cd "$srcdir/$pkgname-${_commit:0:7}"

   sed -e '/m64=/s/lib64/lib/' \
       -i.orig gcc/config/i386/t-linux64
}

build() {
   cd "$srcdir/$pkgname-${_commit:0:7}"

   mkdir -v build
   cd build
   
   SED=sed                                   \
   ../configure --prefix=/usr                \
                --enable-languages=c,c++,go  \
                --enable-shared              \
                --enable-threads=posix       \
                --enable-libmpx              \
                --enable-gnu-unique-object   \
                --enable-linker-build-id     \
                --enable-lto                 \
                --enable-plugin              \
                --enable-install-libiberty   \
                --with-linker-hash-style=gnu \
                --enable-__cxa_atexit        \
                --disable-multilib           \
                --disable-bootstrap          \
                --with-system-zlib
   make
}

package() {
   cd "$srcdir/$pkgname-${_commit:0:7}"

   mkdir -pv $pkgdir/lib
   mkdir -pv $pkgdir/usr/lib
   ln -snf lib $pkgdir/usr/lib64

   cd build
   make DESTDIR="$pkgdir" install

   ln -sfv ../usr/bin/cpp $pkgdir/lib/cpp
   ln -sfv gcc $pkgdir/usr/bin/cc
   install -v -dm755 $pkgdir/usr/lib/bfd-plugins
   ln -sfv ../../libexec/gcc/$(gcc -dumpmachine)/$pkgver/liblto_plugin.so $pkgdir/usr/lib/bfd-plugins/
   mkdir -pv $pkgdir/usr/share/gdb/auto-load/usr/lib
   mv -v $pkgdir/usr/lib/*gdb.py $pkgdir/usr/share/gdb/auto-load/usr/lib
   rm -rf $pkgdir/usr/lib64
}
