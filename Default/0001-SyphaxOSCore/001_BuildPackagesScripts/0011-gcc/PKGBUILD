# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=gcc
pkgver=6.3.0
pkgrel=3
pkgdesc="The GCC package contains the GNU compiler collection, which includes the C and C++ compilers."
arch=('x86_64')
url="http://ftp.gnu.org/gnu/gcc/gcc-$pkgver"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.bz2")
md5sums=('677a7623c7ef6ab99881bc4e048debb6')
depends=('rootfs>=2.0.0'
         'binutils>=2.28'
         'mpc')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   sed -e '/m64=/s/lib64/lib/' -i.orig gcc/config/i386/t-linux64
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   mkdir -v build
   cd build
   SED=sed                                  \
   ../configure --prefix=/usr               \
                --enable-languages=c,c++,go \
                --enable-shared             \
                --disable-multilib          \
                --disable-bootstrap         \
                --enable-checking=release   \
                --with-system-zlib
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   mkdir -pv $pkgdir/lib
   mkdir -pv $pkgdir/usr/lib
   ln -snf lib $pkgdir/usr/lib64

   cd build
   make DESTDIR="$pkgdir" install

   ln -sfv ../usr/bin/cpp $pkgdir/lib/cpp
   ln -sfv gcc $pkgdir/usr/bin/cc
   install -v -dm755 $pkgdir/usr/lib/bfd-plugins
   ln -sfv ../../libexec/gcc/$(gcc -dumpmachine)/$pkgver/liblto_plugin.so $pkgdir/usr/lib/bfd-plugins/
   mkdir -pv $pkgdir/usr/share/gdb/auto-load/usr/lib
   mv -v $pkgdir/usr/lib/*gdb.py $pkgdir/usr/share/gdb/auto-load/usr/lib
   rm -rf $pkgdir/usr/lib64
}
