# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=pciutils
pkgver=3.5.6
pkgrel=2
pkgdesc="PCI bus configuration space access library and tools"
arch=(x86_64)
license=('GPL2')
groups=('code')
url="http://mj.ucw.cz/sw/pciutils/"
depends=('glibc' 'hwids' 'kmod')
source=(https://www.kernel.org/pub/software/utils/pciutils/${pkgname}-${pkgver}.tar.xz)
md5sums=('8e52278399b7b35fcfc031bc05da80a8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make OPT="${CFLAGS} -fPIC -DPIC" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man SBINDIR=/usr/bin lib/libpci.a 
  cp lib/libpci.a "${srcdir}/"
  make clean
  make OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SBINDIR=/usr/bin SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make SHARED=yes PREFIX=/usr SBINDIR=/usr/bin SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" install install-lib
  # this is now supplied by the hwids package
  rm -rf $pkgdir/usr/{sbin/update-pciids,share/{man/man8/update-pciids.8,hwdata}}
}
