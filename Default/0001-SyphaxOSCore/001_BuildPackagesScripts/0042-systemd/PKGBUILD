# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=systemd
pkgver=232
pkgrel=6
pkgdesc="The systemd package contains programs for controlling the startup, running, and shutdown of the system."
arch=('x86_64')
install=$pkgname.install
url="http://anduin.linuxfromscratch.org/sources/other/systemd"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.xz")
md5sums=('281604885d5a87f0359244f6f84037cb')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   sed -i "/seems to be moved/s/^/#/" build-aux/ltmain.sh
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   cc_cv_CFLAGS__flto=no                \
   XSLTPROC="/usr/bin/xsltproc"         \
   ./configure --prefix=/usr            \
               --sysconfdir=/etc        \
               --localstatedir=/var     \
               --with-rootprefix=       \
               --with-rootlibdir=/lib   \
               --enable-split-usr       \
               --disable-firstboot      \
               --disable-ldconfig       \
               --disable-sysusers       \
               --without-python         \
               --with-default-dnssec=no \
               --disable-tests          \
               --docdir=/usr/share/doc/systemd-232
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   make DESTDIR="$pkgdir" install

   mkdir -pv $pkgdir/sbin

   rm -rfv $pkgdir/usr/lib/rpm
   for tool in runlevel reboot shutdown poweroff halt telinit; do
      ln -sfv ../bin/systemctl $pkgdir/sbin/${tool}
   done
   ln -sfv ../lib/systemd/systemd $pkgdir/sbin/init
   
   ln -sfv /lib/libsystemd.la          $pkgdir/usr/lib/libsystemd.la
   ln -sfv /lib/libudev.la             $pkgdir/usr/lib/libudev.la
   ln -sfv /lib/libnss_myhostname.la   $pkgdir/usr/lib/libnss_myhostname.la
   ln -sfv /lib/libnss_mymachines.la   $pkgdir/usr/lib/libnss_mymachines.la
   ln -sfv /lib/libnss_resolve.la      $pkgdir/usr/lib/libnss_resolve.la
   ln -sfv /lib/libnss_systemd.la      $pkgdir/usr/lib/libnss_systemd.la
   
   cp ../../system.conf $pkgdir/etc/systemd/system.conf
   cp ../../systemd-user $pkgdir/etc/pam.d/systemd-user
   
   cd $pkgdir/etc && ln -snf ../run/systemd/resolve/resolv.conf
   
   chmod 2755 $pkgdir/var/log/journal
}
