# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=systemd
pkgver=238
pkgrel=20180531
_commit='f58e62cbbc1c2842881a2a6ab9beda7bcb044a30'
pkgdesc="The systemd package contains programs for controlling the startup, running, and shutdown of the system."
arch=('x86_64')
install=${pkgname}.install
url="https://github.com/systemd/systemd-stable.git"
license=('GPL')
groups=('core')
source=("git+https://github.com/systemd/systemd-stable.git#commit=${_commit}")
md5sums=('SKIP')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname}-stable"

   ## Update Log to Remove Starting Service log trace ==> Keep only started on not Started logs.
   sed -i "s/unit_status_emit_starting_stopping_reloading(u, t)/{}/g" ./src/core/job.c
}

build() {
   cd "$srcdir/${pkgname}-stable"

   XSLTPROC="/usr/bin/xsltproc"        \
   meson build --prefix=/usr           \
               --sysconfdir=/etc       \
               --localstatedir=/var    \
               -Dfallback-hostname='SyphaxOS' \
               -Dblkid=true            \
               -Dbuildtype=release     \
               -Drootprefix=           \
               -Drootlibdir=/lib       \
               -Dsplit-usr=true        \
               -Dfirstboot=false       \
               -Dldconfig=false        \
               -Dsysusers=false        \
               -Daudit=false           \
               -Ddefault-dnssec=no     \
               -Dinstall-tests=false   \
               -Db_lto=false           \
               -Dtests=unsafe

   ninja -C build
}

package() {
   cd "$srcdir/${pkgname}-stable"

   DESTDIR="${pkgdir}" ninja -C build install
   
   mkdir -pv $pkgdir/sbin

   rm -rfv $pkgdir/usr/lib/rpm
   for tool in runlevel reboot shutdown poweroff halt telinit; do
      ln -sfv ../bin/systemctl $pkgdir/sbin/${tool}
   done
   
   ln -sfv ../lib/systemd/systemd $pkgdir/sbin/init
      
   cp ../../system.conf $pkgdir/etc/systemd/system.conf
   cp ../../systemd-user $pkgdir/etc/pam.d/systemd-user
     
   chmod 2755 $pkgdir/var/log/journal
   chmod 700  $pkgdir/usr/share/polkit-1/rules.d
}
