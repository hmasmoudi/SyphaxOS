# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=systemd
pkgver=235
pkgrel=4
pkgdesc="The systemd package contains programs for controlling the startup, running, and shutdown of the system."
arch=('x86_64')
install=$pkgname.install
url="https://github.com/systemd/systemd/archive"
license=('GPL')
groups=('core')
source=($pkgname-$pkgver.tar.gz::"$url/v$pkgver.tar.gz")
md5sums=('SKIP')
depends=('rootfs')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   ## Update Log to Remove Starting Service log trace ==> Keep only started on not Started logs.
   sed -i "s/unit_status_emit_starting_stopping_reloading(u, t)/{}/g" ./src/core/job.c
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   cc_cv_CFLAGS__flto=no               \
   XSLTPROC="/usr/bin/xsltproc"        \
   meson build --prefix=/usr           \
               --sysconfdir=/etc       \
               --localstatedir=/var    \
               -Dfallback-hostname='SyphaxOS' \
               -Drootprefix=           \
               -Drootlibdir=/lib       \
               -Dsplit-usr=true        \
               -Dfirstboot=false       \
               -Dldconfig=false        \
               -Dsysusers=false        \
               -Daudit=false           \
               -Ddefault-dnssec=no     \
               -Dtests=unsafe

   ninja  -C build -j$(($(getconf _NPROCESSORS_ONLN)+1))
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   DESTDIR="${pkgdir}" ninja -C build install
   
   mkdir -pv $pkgdir/sbin

   rm -rfv $pkgdir/usr/lib/rpm
   for tool in runlevel reboot shutdown poweroff halt telinit; do
      ln -sfv ../bin/systemctl $pkgdir/sbin/${tool}
   done
   
   ln -sfv ../lib/systemd/systemd $pkgdir/sbin/init
      
   cp ../../system.conf $pkgdir/etc/systemd/system.conf
   cp ../../systemd-user $pkgdir/etc/pam.d/systemd-user
     
   chmod 2755 $pkgdir/var/log/journal
   chmod 700  $pkgdir/usr/share/polkit-1/rules.d
}
