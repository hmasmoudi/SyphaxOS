# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=kmod
pkgver=24
pkgrel=4
pkgdesc="The Kmod package contains libraries and utilities for loading kernel modules."
arch=('x86_64')
url="https://www.kernel.org/pub/linux/utils/kernel/kmod"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.xz")
md5sums=('08297dfb6f2b3f625f928ca3278528af')
depends=('rootfs')

build() {
   cd "$srcdir/$pkgname-$pkgver"

   ./configure --prefix=/usr          \
               --bindir=/bin          \
               --sysconfdir=/etc      \
               --with-rootlibdir=/lib \
               --with-xz              \
               --with-zlib

   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   make DESTDIR="$pkgdir" install

   mkdir -pv $pkgdir/sbin
   mkdir -pv $pkgdir/bin

   for target in depmod insmod lsmod modinfo modprobe rmmod; do
    ln -sv ../bin/kmod $pkgdir/sbin/$target
   done

   ln -sv kmod $pkgdir/bin/lsmod
}
