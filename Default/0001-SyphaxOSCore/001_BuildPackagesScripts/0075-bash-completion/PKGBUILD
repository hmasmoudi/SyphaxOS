# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=bash-completion
pkgver=2.7
pkgrel=1
pkgdesc="Programmable completion for the bash shell"
arch=('x86_64')
url="https://github.com/scop/bash-completion"
license=('GPL2')
depends=('bash')
source=("https://github.com/scop/bash-completion/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha1sums=('SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
  rm "${pkgdir}/etc/profile.d/bash_completion.sh"

  # remove Slackware's makepkg completion
  rm "${pkgdir}/usr/share/bash-completion/completions/makepkg"

  # remove completions which overlap with util-linux
  rm "${pkgdir}/usr/share/bash-completion/completions"/{u,}mount
}
