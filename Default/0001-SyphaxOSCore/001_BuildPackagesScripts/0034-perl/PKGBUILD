# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=perl
pkgver=5.26.1
pkgrel=4
pkgdesc="The Perl package contains the Practical Extraction and Report Language."
arch=('x86_64')
url="https://www.cpan.org/src/5.0"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.xz")
sha256sums=('fe8208133e73e47afc3251c08d2c21c5a60160165a8ab8b669c43a420e4ec680')
depends=('rootfs')

build() {
   cd "$srcdir/$pkgname-$pkgver"

   export BUILD_ZLIB=False
   export BUILD_BZIP2=0

   sh Configure -des -Dprefix=/usr                 \
                     -Dvendorprefix=/usr           \
                     -Dman1dir=/usr/share/man/man1 \
                     -Dman3dir=/usr/share/man/man3 \
                     -Dpager="/usr/bin/less -isR"  \
                     -Duseshrplib                  \
                     -Dusethreads


   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   export BUILD_ZLIB=False
   export BUILD_BZIP2=0

   make DESTDIR="$pkgdir" install
}
