# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=inetutils
pkgver=1.9.4
pkgrel=3
pkgdesc="The Inetutils package contains programs for basic networking."
arch=('x86_64')
url="http://ftp.gnu.org/gnu/inetutils"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.xz")
md5sums=('87fef1fa3f603aef11c41dcc097af75e')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   ./configure --prefix=/usr        \
               --localstatedir=/var \
               --disable-logger     \
               --disable-whois      \
               --disable-rcp        \
               --disable-rexec      \
               --disable-rlogin     \
               --disable-rsh        \
               --disable-servers

   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   make DESTDIR="$pkgdir" install

   mkdir -p $pkgdir/bin
   mkdir -p $pkgdir/sbin

   mv -v $pkgdir/usr/bin/{hostname,ping,ping6,traceroute} $pkgdir/bin
   mv -v $pkgdir/usr/bin/ifconfig $pkgdir/sbin
}
