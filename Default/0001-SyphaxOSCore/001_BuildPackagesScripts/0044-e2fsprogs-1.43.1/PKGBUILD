# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=e2fsprogs
pkgver=1.43.1
pkgrel=1
pkgdesc="The E2fsprogs package contains the utilities for handling the ext2, ext3 and ext4 file system."
arch=('x86_64')
url="http://downloads.sourceforge.net/project/e2fsprogs/e2fsprogs/v$pkgver"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.gz")
md5sums=('1775f3f0eed9dee1c5f39e08d1964a97')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   mkdir -v build
   cd build

   ../configure --prefix=/usr           \
                --bindir=/bin           \
                --with-root-prefix=""   \
                --enable-elf-shlibs     \
                --disable-libblkid      \
                --disable-libuuid       \
                --disable-uuidd         \
                --disable-fsck

   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   cd build

   make DESTDIR="$pkgdir" install

   make DESTDIR="$pkgdir" install-libs

   chmod -v u+w $pkgdir/usr/lib/{libcom_err,libe2p,libext2fs,libss}.a
   gunzip -v $pkgdir/usr/share/info/libext2fs.info.gz
   install-info --dir-file=$pkgdir/usr/share/info/dir $pkgdir/usr/share/info/libext2fs.info
   makeinfo -o      doc/com_err.info ../lib/et/com_err.texinfo
   install -v -m644 doc/com_err.info $pkgdir/usr/share/info
   install-info --dir-file=$pkgdir/usr/share/info/dir $pkgdir/usr/share/info/com_err.info
}
