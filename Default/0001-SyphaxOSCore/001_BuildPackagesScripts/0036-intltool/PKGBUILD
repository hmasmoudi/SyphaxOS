# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=intltool
pkgver=0.51.0
pkgrel=6
pkgdesc="The Intltool is an internationalization tool used for extracting translatable strings from source files."
arch=('x86_64')
url="http://launchpad.net/intltool/trunk/0.51.0/+download"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.gz"
        "intltool-0.51.0-perl-5.26.patch"
        "intltool-merge-Create-cache-file-atomically.patch"
        "intltool_distcheck-fix.patch")
sha256sums=('67c74d94196b153b774ab9f89b2fa6c6ba79352407037c8c14d5aeb334e959cd'
            '458f5d53ac358810879080f69b11649901babcacc14471c243eb850e9436d546'
            '13bd6deb65dc94933f132919d4eea4c24354d7c1c1c9e5930cb6e70c75703763'
            '58cb7d60796bc69c7d0865f106feb589d0271619e62f8741bff6f5ce1a2615ff')
depends=('rootfs')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   patch -Np1 -i ../intltool-0.51.0-perl-5.26.patch
   patch -Np1 -i ../intltool-merge-Create-cache-file-atomically.patch
   patch -Np1 -i ../intltool_distcheck-fix.patch
   
   sed -i 's:\\\${:\\\$\\{:' intltool-update.in
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   ./configure --prefix=/usr

   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   make DESTDIR="$pkgdir" install

   install -v -Dm644 doc/I18N-HOWTO $pkgdir/usr/share/doc/intltool-$pkgver/I18N-HOWTO
}
