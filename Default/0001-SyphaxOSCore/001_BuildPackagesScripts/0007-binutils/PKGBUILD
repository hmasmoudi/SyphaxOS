# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=binutils
pkgver=2.28
pkgrel=2
pkgdesc="The Binutils package contains a linker, an assembler, and other tools for handling object files."
arch=('x86_64')
url="ftp://ftp.gnu.org/gnu/binutils"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.bz2")
md5sums=('9e8340c96626b469a603c15c9d843727')
depends=('rootfs>=2.0.0'
         'glibc>=2.24'
         'zlib')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
   
   mkdir -v build
}

build() {
   cd "$srcdir/$pkgname-$pkgver"
   cd       build

   ../configure --prefix=/usr       \
                --enable-gold       \
                --enable-ld=default \
                --enable-plugins    \
                --enable-shared     \
                --disable-werror    \
                --with-system-zlib

   make tooldir=/usr
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   cd       build

   make DESTDIR="$pkgdir" tooldir=/usr install
}
