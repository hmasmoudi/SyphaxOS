# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=bzip2
pkgver=1.0.6
pkgrel=3
pkgdesc="The Bzip2 package contains programs for compressing and decompressing files."
arch=('x86_64')
url="http://www.bzip.org/1.0.6"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.gz")
md5sums=('00b516f4704d4a7cb50a1d97e6e8e15b')
depends=('rootfs>=2.0.0'
         'glibc')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   patch -Np1 -i ../../bzip2-1.0.6-install_docs-1.patch

   sed -i 's@\(ln -s -f \)$(PREFIX)/bin/@\1@' Makefile
   sed -i "s@(PREFIX)/man@(PREFIX)/share/man@g" Makefile
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   make -f Makefile-libbz2_so
   make clean
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   make PREFIX="$pkgdir"/usr install

   mkdir -pv $pkgdir/{bin,lib}
   cp -v bzip2-shared $pkgdir/bin/bzip2
   cp -av libbz2.so* $pkgdir/lib
   ln -sv ../../lib/libbz2.so.1.0 $pkgdir/usr/lib/libbz2.so
   rm -v $pkgdir/usr/bin/{bunzip2,bzcat,bzip2}
   ln -sv bzip2 $pkgdir/bin/bunzip2
   ln -sv bzip2 $pkgdir/bin/bzcat
}
