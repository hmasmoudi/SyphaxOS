# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=os-prober
pkgver=1.76
pkgrel=1
pkgdesc="Detect OSes and linux variants."
arch=('x86_64')
url="http://http.debian.net/debian/pool/main/o/os-prober"
license=('GPL')
groups=('core')
source=("$url/${pkgname}_${pkgver}.tar.xz")
sha256sums=('SKIP')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   # adjust lib dir to allow detection of 64-bit distros
   sed -i -e "s:/lib/ld\*\.so\*:/lib*/ld*.so*:g" os-probes/mounted/common/90linux-distro

   rm -f Makefile
}


build() {
   cd "$srcdir/$pkgname-$pkgver"

   make newns
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   install -Dm755 linux-boot-prober "$pkgdir"/usr/bin/linux-boot-prober
   install -Dm755 os-prober "$pkgdir"/usr/bin/os-prober
   install -Dm755 newns "$pkgdir"/usr/lib/os-prober/newns
   install -Dm755 common.sh $pkgdir/usr/share/os-prober/common.sh  

   for dir in os-probes os-probes/mounted os-probes/init linux-boot-probes linux-boot-probes/mounted; do
      install -dm755 "$pkgdir/usr/lib/$dir"
      install -m755 -t "$pkgdir/usr/lib/$dir" "$dir"/common/*
      [[ -d "$dir"/x86 ]] && cp -r "$dir"/x86/* "$pkgdir/usr/lib/$dir"
   done

   install -Dm755 os-probes/mounted/powerpc/20macosx "$pkgdir"/usr/lib/os-probes/mounted/20macosx

   install -dm755 "$pkgdir"/var/lib/os-prober
}

