# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=lzo
pkgver=2.10
pkgrel=4
pkgdesc="Portable lossless data compression library"
arch=('x86_64')
url="http://www.oberhumer.com/opensource/lzo/download"
license=('GPL')
groups=('core')
source=("$url/${pkgname}-${pkgver}.tar.gz")
md5sums=('39d3f3f9c55c87b1e5d6888e1420f4b5')
depends=('rootfs')

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr --enable-shared
   make

   # build minilzo
   gcc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
   gcc $LDFLAGS -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
   
   # install minilzo
   install -m 755 libminilzo.so.0 ${pkgdir}/usr/lib
   install -p -m 644 minilzo/minilzo.h ${pkgdir}/usr/include/lzo
   cd ${pkgdir}/usr/lib
   ln -s libminilzo.so.0 libminilzo.so
}

