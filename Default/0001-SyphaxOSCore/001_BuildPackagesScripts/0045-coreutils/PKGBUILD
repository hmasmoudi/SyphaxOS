# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=coreutils
pkgver=8.25
pkgrel=2
pkgdesc="The Coreutils package contains utilities for showing and setting the basic system characteristics."
arch=('x86_64')
url="http://ftp.gnu.org/gnu/coreutils"
license=('GPL')
groups=('core')
source=("$url/$pkgname-$pkgver.tar.xz")
md5sums=('070e43ba7f618d747414ef56ab248a48')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"

   patch -Np1 -i ../../coreutils-8.25-i18n-2.patch
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   FORCE_UNSAFE_CONFIGURE=1 ./configure \
               --prefix=/usr            \
               --enable-no-install-program=kill,uptime

   FORCE_UNSAFE_CONFIGURE=1 make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"

   make DESTDIR="$pkgdir" install

   mkdir -pv $pkgdir/bin
   mkdir -pv $pkgdir/usr/sbin
   mkdir -pv $pkgdir/usr/share/man/man8

   mv -v $pkgdir/usr/bin/{cat,chgrp,chmod,chown,cp,date,dd,df,echo} $pkgdir/bin
   mv -v $pkgdir/usr/bin/{false,ln,ls,mkdir,mknod,mv,pwd,rm} $pkgdir/bin
   mv -v $pkgdir/usr/bin/{rmdir,stty,sync,true,uname} $pkgdir/bin
   mv -v $pkgdir/usr/bin/chroot $pkgdir/usr/sbin
   mv -v $pkgdir/usr/share/man/man1/chroot.1 $pkgdir/usr/share/man/man8/chroot.8
   sed -i s/\"1\"/\"8\"/1 $pkgdir/usr/share/man/man8/chroot.8
   mv -v $pkgdir/usr/bin/{head,sleep,nice,test,[} $pkgdir/bin
}
