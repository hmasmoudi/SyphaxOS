# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=mesa
pkgver=17.3.3
pkgrel=2
pkgdesc="Mesa is an OpenGL compatible 3D graphics library."
arch=('x86_64')
url="https://mesa.freedesktop.org/archive"
license=('GPL')
groups=('graphicslayer')
source=("$url/${pkgname}-${pkgver}.tar.xz")
sha256sums=('41bac5de0ef6adc1f41a1ec0f80c19e361298ce02fa81b5f9ba4fdca33a9379b')
makedepends=('python2-mako')
depends=('rootfs')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
   
   autoreconf -fiv

   sed -i "/seems to be moved/s/^/: #/" bin/ltmain.sh
}

build() {
   cd "$srcdir/$pkgname-$pkgver"
   
   source /etc/profile.d/xorg.sh
   echo "Using XORG_PREFIX ($XORG_PREFIX) & XORG_CONFIG ($XORG_CONFIG)"

   ./configure  --prefix=$XORG_PREFIX  \
                --sysconfdir=/etc \
                --with-gallium-drivers=r600,nouveau,svga,virgl \
                --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
                --with-platforms=drm,wayland,x11 \
                --with-vulkan-drivers=intel \
                --enable-shared-glapi \
                --enable-egl \
                --enable-glx \
                --enable-glx-tls \
                --enable-gles1 \
                --enable-gles2 \
                --enable-gbm \
                --enable-dri \
                --enable-dri3 \
                --enable-gallium-extra-hud \
                --enable-texture-float \
                --enable-xa \
                --enable-vdpau \
                --enable-osmesa
   
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   
   #Make the package
   make DESTDIR="$pkgdir" install
}

