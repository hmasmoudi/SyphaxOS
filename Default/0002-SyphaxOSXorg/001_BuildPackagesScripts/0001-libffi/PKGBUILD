# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=libffi
pkgver=3.2.1
pkgrel=2
pkgdesc="The libffi library provides a portable, high level programming interface to various calling conventions."
arch=('x86_64')
url="ftp://sourceware.org/pub/libffi"
license=('GPL')
groups=('graphicslayer')
source=("$url/${pkgname}-${pkgver}.tar.gz")
md5sums=('83b89587607e3eb65c70d361f13bab43')
depends=('rootfs')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
   
   sed -e '/^includesdir/ s/$(libdir).*$/$(includedir)/' \
       -i include/Makefile.in

   sed -e '/^includedir/ s/=.*$/=@includedir@/' \
       -e 's/^Cflags: -I${includedir}/Cflags:/' \
       -i libffi.pc.in
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   ./configure --prefix=/usr --disable-static
   
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   
   #Create Needed System Link needed for package install
   mkdir -p $pkgdir/lib
   mkdir -p $pkgdir/usr/lib
   ln -snfv lib $pkgdir/lib64
   ln -snfv lib $pkgdir/usr/lib64

   #Make the package
   make DESTDIR="$pkgdir" install
   
   #Remove Used System Link during package install
   rm -rf $pkgdir/lib64
   rm -rf $pkgdir/usr/lib64
}

