# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=mesa
pkgver=17.0.4
pkgrel=1
pkgdesc="Mesa is an OpenGL compatible 3D graphics library."
arch=('x86_64')
url="https://mesa.freedesktop.org/archive"
license=('GPL')
groups=('graphicslayer')
source=("$url/${pkgname}-${pkgver}.tar.xz")
sha256sums=('1269dc8545a193932a0779b2db5bce9be4a5f6813b98c38b93b372be8362a346')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
   
   sed -i "/pthread_stubs_possible=/s/yes/no/" configure.ac
   sed -i "/seems to be moved/s/^/: #/" bin/ltmain.sh
}

build() {
   cd "$srcdir/$pkgname-$pkgver"
   
   source /etc/profile.d/xorg.sh
   echo "Using XORG_PREFIX ($XORG_PREFIX) & XORG_CONFIG ($XORG_CONFIG)"

   ./autogen.sh CFLAGS='-O2' CXXFLAGS='-O2' \
         --prefix=$XORG_PREFIX           \
         --sysconfdir=/etc               \
         --enable-texture-float          \
         --enable-gles1                  \
         --enable-gles2                  \
         --enable-osmesa                 \
         --enable-xa                     \
         --enable-gbm                    \
         --enable-glx-tls                \
         --with-egl-platforms="drm,x11,wayland"  \
         --without-gallium-drivers
   
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   
   #Make the package
   make DESTDIR="$pkgdir" install
}

