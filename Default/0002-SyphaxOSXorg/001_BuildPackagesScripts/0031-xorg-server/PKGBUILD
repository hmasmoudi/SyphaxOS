# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=xorg-server
pkgver=1.19.5
pkgrel=4
pkgdesc="The Xorg Server is the graphicslayer of the X Window system. "
arch=('x86_64')
url="http://ftp.x.org/pub/individual/xserver"
license=('GPL')
groups=('graphicslayer')
source=("$url/${pkgname}-${pkgver}.tar.bz2"
        "nvidia-add-modulepath-support.patch"
        "xserver-autobind-hotplug.patch"
        "xvfb-run"
        "xvfb-run.1")
sha256sums=('18fffa8eb93d06d2800d06321fc0df4d357684d8d714315a66d8dfa7df251447'
            '914a8d775b708f836ae3f0eeca553da3872727a2e4262190f4d5c01241cb14e8'
            'fcaf536e4fc307958923b58f2baf3d3102ad694efc28506f6f95a9e64483fa57'
            'ff0156309470fc1d378fd2e104338020a884295e285972cc88e250e031cc35b9'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776')
            
depends=('rootfs')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
   
   # merged upstream in trunk
   patch -Np1 -i ../nvidia-add-modulepath-support.patch
   # patch from Fedora, not yet merged
   patch -Np1 -i ../xserver-autobind-hotplug.patch
  
   autoreconf -vfi

   sed -i "/seems to be moved/s/^/#/" ltmain.sh
}

build() {
   cd "$srcdir/$pkgname-$pkgver"
   
   source /etc/profile.d/xorg.sh
   echo "Using XORG_PREFIX ($XORG_PREFIX) & XORG_CONFIG ($XORG_CONFIG)"

   # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
   # With them, module fail to load with undefined symbol.
   # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
   export CFLAGS=${CFLAGS/-fno-plt}
   export CXXFLAGS=${CXXFLAGS/-fno-plt}
   export LDFLAGS=${LDFLAGS/,-z,now}
  
   ./configure $XORG_CONFIG \
               --libdir=/usr/lib \
               --enable-glamor \
               --enable-suid-wrapper \
               --enable-dri \
               --enable-dmx \
               --enable-xvfb \
               --enable-xnest \
               --enable-composite \
               --enable-xcsecurity \
               --enable-xorg \
               --enable-xephyr \
               --enable-glamor \
               --enable-xwayland \
               --disable-xfbdev \
               --disable-xfake \
               --disable-static \
               --with-sha1=libgcrypt \
               --enable-systemd-logind \
              --with-xkb-output=/var/lib/xkb
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   
   #Make the package
   make DESTDIR="$pkgdir" install
   
   mkdir -pv $pkgdir/etc/X11/xorg.conf.d
}

