# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=xorg-server
pkgver=1.19.3
pkgrel=6
pkgdesc="The Xorg Server is the graphicslayer of the X Window system. "
arch=('x86_64')
url="http://ftp.x.org/pub/individual/xserver"
license=('GPL')
groups=('graphicslayer')
source=("$url/${pkgname}-${pkgver}.tar.bz2"
        "nvidia-add-modulepath-support.patch"
        "xserver-autobind-hotplug.patch"
        "modesetting-Set-correct-DRM-event-context-version.patch"
        "CVE-2017-10971.patch"
        "CVE-2017-10972.patch"
        "bug99708.patch"
        "xvfb-run"
        "xvfb-run.1")
sha256sums=('677a8166e03474719238dfe396ce673c4234735464d6dadf2959b600d20e5a98'
            '914a8d775b708f836ae3f0eeca553da3872727a2e4262190f4d5c01241cb14e8'
            'fcaf536e4fc307958923b58f2baf3d3102ad694efc28506f6f95a9e64483fa57'
            '831a70809e6bec766138d7a1c96643732df9a2c0c5f77ee44b47ce4be882e0af'
            '3950d5d64822b4a34ca0358389216eed25e159751006d674e7cb491aa3b54d0b'
            '700af48c541f613b376eb7a7e567d13c0eba7a835d0aaa9d4b0431ebdd9f397c'
            '67013743ba8ff1663b233f50fae88989d36504de83fca5f93af5623f2bef6920'
            'ff0156309470fc1d378fd2e104338020a884295e285972cc88e250e031cc35b9'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776')
            
depends=('rootfs')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
   
   # merged upstream in trunk
   patch -Np1 -i ../nvidia-add-modulepath-support.patch
   # patch from Fedora, not yet merged
   patch -Np1 -i ../xserver-autobind-hotplug.patch
   # merged in trunk
   patch -Np1 -i ../modesetting-Set-correct-DRM-event-context-version.patch

   patch -Np1 -i ../CVE-2017-10971.patch
   patch -Np1 -i ../CVE-2017-10972.patch

   # https://bugs.archlinux.org/task/53404
   patch -Np1 -i ../bug99708.patch
  
   autoreconf -vfi

   sed -i "/seems to be moved/s/^/#/" ltmain.sh
}

build() {
   cd "$srcdir/$pkgname-$pkgver"
   
   source /etc/profile.d/xorg.sh
   echo "Using XORG_PREFIX ($XORG_PREFIX) & XORG_CONFIG ($XORG_CONFIG)"

   ./configure $XORG_CONFIG            \
               --enable-glamor          \
               --enable-suid-wrapper    \
               --enable-dri \
               --enable-dmx \
               --enable-xvfb \
               --enable-xnest \
               --enable-composite \
               --enable-xcsecurity \
               --enable-xorg \
               --enable-xephyr \
               --enable-glamor \
               --enable-xwayland \
              --with-xkb-output=/var/lib/xkb
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   
   #Make the package
   make DESTDIR="$pkgdir" install
   
   mkdir -pv $pkgdir/etc/X11/xorg.conf.d
}

