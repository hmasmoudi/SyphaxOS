# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=xf86-video-intel
pkgver=20180124
pkgrel=4
_commit=26f5406841f3924f23f29df61b5ea53d2816b665 # master
pkgdesc="The Xorg Intel Driver package contains the X.Org Video Driver for Intel integrated video cards"
arch=('x86_64')
url="https://01.org/linuxgraphics"
license=('GPL')
groups=('graphicslayer')
source=("git+https://anongit.freedesktop.org/git/xorg/driver/xf86-video-intel#commit=$_commit")
md5sums=('SKIP')
depends=('rootfs')

prepare() {
   cd "$srcdir/$pkgname"
   NOCONFIGURE=1 ./autogen.sh
}

build() {
   cd "$srcdir/$pkgname"
   
   source /etc/profile.d/xorg.sh
   echo "Using XORG_PREFIX ($XORG_PREFIX) & XORG_CONFIG ($XORG_CONFIG)"

   export CFLAGS=${CFLAGS/-fno-plt}
   export CXXFLAGS=${CXXFLAGS/-fno-plt}
   export LDFLAGS=${LDFLAGS/,-z,now}

   ./configure $XORG_CONFIG --with-default-dri=3 --enable-glamor
   
   make
}

package() {
   cd "$srcdir/$pkgname"
   
   #Make the package
   make DESTDIR="$pkgdir" install
}

