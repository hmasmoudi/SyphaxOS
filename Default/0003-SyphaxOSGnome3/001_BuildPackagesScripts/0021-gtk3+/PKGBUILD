# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=gtk3
pkgver=3.22.30
pkgrel=2
pkgdesc="The GTK+ 3 package contains libraries used for creating graphical user interfaces for applications."
arch=('x86_64')
url="https://ftp.gnome.org/pub/gnome/sources/gtk+/3.22"
license=('GPL')
groups=('desktop')
source=("$url/gtk+-$pkgver.tar.xz")
sha256sums=('a1a4a5c12703d4e1ccda28333b87ff462741dc365131fbc94c218ae81d9a6567')
depends=('rootfs')
makedepends=(gobject-introspection libcanberra colord rest sassc)

prepare() {
   cd "$srcdir/gtk+-$pkgver"
      
   NOCONFIGURE=1 ./autogen.sh
   
   sed -i "/seems to be moved/s/^/#/" build-aux/ltmain.sh
}

build() {
   cd "$srcdir/gtk+-$pkgver"

   ./configure --prefix=/usr             \
               --sysconfdir=/etc         \
               --enable-broadway-backend \
               --enable-wayland-backend  \
               --enable-x11-backend      \
               --enable-xdamage          \
               --enable-xcomposite       \
               --enable-xrandr           \
               --enable-xkb              \
               --enable-xinerama         \
               --disable-glibtest        \
               --disable-schemas-compile

   #https://bugzilla.gnome.org/show_bug.cgi?id=655517
   sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
   
   make
}

package() {
   cd "$srcdir/gtk+-$pkgver"

   make DESTDIR="$pkgdir" install
   
   cp ../../settings.ini ${pkgdir}/etc/gtk-3.0/settings.ini
}

