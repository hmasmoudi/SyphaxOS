# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=pygobject
pkgver=3.26.1
pkgrel=2
pkgdesc="PyGObject 2 and 3 provides Python bindings to the GObject class from GLib"
arch=('x86_64')
url="https://ftp.gnome.org/pub/gnome/sources/pygobject/3.26"
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f5577b9b9c70cabb9a60d81b855d488b767c66f867432e7fb64aa7269b04d1a9')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   mkdir python2
   mkdir python3
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"

   pushd python2
   ../configure --prefix=/usr --with-python=/usr/bin/python
   make
   popd
   
   pushd python3
   ../configure --prefix=/usr --with-python=/usr/bin/python3
   make
   popd
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make -C python2 DESTDIR="$pkgdir" install
   make -C python3 DESTDIR="$pkgdir" install
}

