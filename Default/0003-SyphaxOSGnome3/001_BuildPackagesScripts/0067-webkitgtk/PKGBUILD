# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=webkitgtk
pkgver=2.16.6
pkgrel=4
pkgdesc="GTK+ Web content engine library"
arch=('x86_64')
url="https://webkitgtk.org/"
license=(custom)
depends=('rootfs' 'hyphen')
source=(https://webkitgtk.org/releases/${pkgname}-${pkgver}.tar.xz)
sha256sums=('fc23650df953123c59b9c0edf3855e7bd55bd107820997fc72375811e1ea4b21')

prepare() {
   mkdir build

   cd webkitgtk-$pkgver
   sed -i '1s/python$/&2/' Tools/gtk/generate-gtkdoc
   rm -r Source/ThirdParty/gtest/

   #Fix a build issue with ICU >= 59
   sed -i 's/unsigned short/char16_t/'            \
          Source/JavaScriptCore/API/JSStringRef.h \
          Source/WebKit2/Shared/API/c/WKString.h  &&

   sed -i '/stdbool.h/ a#include <uchar.h>' \
          Source/JavaScriptCore/API/JSBase.h
}

build() {
   cd build
   CFLAGS=-Wno-expansion-to-defined  \
   CXXFLAGS=-Wno-expansion-to-defined \
   cmake -DPORT=GTK -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_INSTALL_DIR=/usr/lib -DLIBEXEC_INSTALL_DIR=/usr/lib/webkit2gtk-4.0 \
        -DENABLE_GTKDOC=OFF -DPYTHON_EXECUTABLE=/usr/bin/python2 ../webkitgtk-$pkgver
   make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/webkitgtk"
  cd "$srcdir/webkitgtk-$pkgver/Source"
  for f in $(find -name 'COPYING*' -or -name 'LICENSE*'); do
    echo $f >> "$pkgdir/usr/share/licenses/webkitgtk/LICENSE"
    cat $f >> "$pkgdir/usr/share/licenses/webkitgtk/LICENSE"
    echo "" >> "$pkgdir/usr/share/licenses/webkitgtk/LICENSE"
  done
}
