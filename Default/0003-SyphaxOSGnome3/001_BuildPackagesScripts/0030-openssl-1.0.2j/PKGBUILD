# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=openssl
pkgver=1.1.0e
pkgrel=1
pkgdesc="The OpenSSL package contains management tools and libraries relating to cryptography."
arch=('x86_64')
url="https://openssl.org/source"
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.gz"
        "https://hg.mozilla.org/mozilla-central/raw-file/tip/security/nss/lib/ckfw/builtins/certdata.txt"
        "remove-expired-certs.sh"
        "make-ca.sh"
        "make-cert.pl")
sha1sums=('8bbbaf36feffadd3cb9110912a8192e665ebca4b'
          'SKIP'
          '0c5c4a49307ee52ba328a14cd003d8d670648adf'
          '93e4634b66b08a4f0da949b26972dad1c079ad06'
          'afc80928c9d07471583043bb897e60cbe5375c4c')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
    
   cp "${srcdir}"/certdata.txt            "${srcdir}"/$pkgname-$pkgver
   cp "${srcdir}"/remove-expired-certs.sh "${srcdir}"/$pkgname-$pkgver
   cp "${srcdir}"/make-ca.sh              "${srcdir}"/$pkgname-$pkgver
   cp "${srcdir}"/make-cert.pl            "${srcdir}"/$pkgname-$pkgver
   
   sed -i '1 i #CVS_ID @# $ RCSfile: certdata.txt $ $Revision: 20170330 $ $Date: $' certdata.txt
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   ./config --prefix=/usr \
            --openssldir=/etc/ssl \
            --libdir=lib \
            shared \
            zlib-dynamic
   
   #disable static libraries
   sed -i 's#=libcrypto.a libssl.a#=#' Makefile

   make depend   
   make -j1
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   
   install -dv -m755 $pkgdir/usr/share/man
   
   make DESTDIR=$pkgdir MANDIR=/usr/share/man MANSUFFIX=ssl install
   
   install -dv -m755 $pkgdir/usr/share/doc/$pkgname-$pkgver
   cp -vfr doc/*     $pkgdir/usr/share/doc/$pkgname-$pkgver
 
   sh make-ca.sh
   
   SSLDIR=$pkgdir/etc/ssl
   sh remove-expired-certs.sh certs
   install -d ${SSLDIR}/certs
   cp -v certs/*.pem ${SSLDIR}/certs
   c_rehash
   install BLFS-ca-bundle*.crt ${SSLDIR}/ca-bundle.crt
   ln -sfv ../ca-bundle.crt ${SSLDIR}/certs/ca-certificates.crt
   unset SSLDIR
}

