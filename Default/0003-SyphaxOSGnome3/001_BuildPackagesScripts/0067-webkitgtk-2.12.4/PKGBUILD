# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=webkitgtk
pkgver=2.12.4
pkgrel=1
pkgdesc="The WebKitGTK+ package is a port of the portable web rendering engine WebKit to the GTK+ 3 and GTK+ 2 platforms."
arch=('x86_64')
url="http://webkitgtk.org/releases"
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.xz")
md5sums=('0331ac714fd1a0587ffea470ef1afb20')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver"
   
   sed -e 's/“/\"/' -e 's/”/\"/' \
       -i Source/WebCore/xml/XMLViewer.{css,js} 
}

build() {
   cd "$srcdir/$pkgname-$pkgver"

   mkdir -vp build
   cd        build

   cmake -DCMAKE_BUILD_TYPE=Release  \
         -DCMAKE_INSTALL_PREFIX=/usr \
         -DCMAKE_SKIP_RPATH=ON       \
         -DPORT=GTK                  \
         -DLIB_INSTALL_DIR=/usr/lib  \
         -DUSE_LIBHYPHEN=OFF         \
         -DENABLE_MINIBROWSER=ON     \
         -Wno-dev ..
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   
   cd  build

   make DESTDIR="$pkgdir" install
   
   install -vdm755 $pkgdir/usr/share/gtk-doc/html/webkit{2,dom}gtk-4.0
   
   install -vm644  ../Documentation/webkit2gtk-4.0/html/*   \
                   $pkgdir/usr/share/gtk-doc/html/webkit2gtk-4.0
                   
   install -vm644  ../Documentation/webkitdomgtk-4.0/html/* \
                   $pkgdir/usr/share/gtk-doc/html/webkitdomgtk-4.0
}

