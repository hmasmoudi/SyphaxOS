# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=newt
pkgver=0.52.20
pkgrel=4
pkgdesc="Newt is a programming library for color text mode, widget based user interfaces."
arch=('x86_64')
url="https://distfiles.macports.org/libnewt"
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.gz")
md5sums=('SKIP')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -e 's/^LIBNEWT =/#&/' \
       -e '/install -m 644 $(LIBNEWT)/ s/^/#/' \
       -e 's/$(LIBNEWT)/$(LIBNEWTSONAME)/g' \
       -i Makefile.in
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr --with-gpm-support
   
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
}

