# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=cups
pkgver=2.1.4
pkgrel=1
pkgdesc="The Common Unix Printing System (CUPS) is a print spooler and associated utilities."
arch=('x86_64')
url="https://github.com/apple/cups/releases/download/release-${pkgver}"
install=${pkgname}.install
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}-source.tar.gz"
        "${pkgname}.install"
        "${pkgname}")
md5sums=('9f9bf6e3b9c20a3519b4dc409666d6e7'
         '1afea37242273f58d4229003d1ae7d96'
         '26ec8039dbcb9b470ef1449f08f951a6')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -i 's:555:755:g;s:444:644:g' Makedefs.in
   sed -i '/MAN.EXT/s:.gz::g' configure config-scripts/cups-manpages.m4
   sed -i '/LIBGCRYPTCONFIG/d' config-scripts/cups-ssl.m4
   aclocal  -I config-scripts
   autoconf -I config-scripts
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   CC=gcc \
   ./configure --libdir=/usr/lib            \
               --with-rcdir=/tmp/cupsinit   \
               --with-system-groups=lpadmin \
               --with-docdir=/usr/share/cups/doc-${pkgver}
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   mkdir -pv "${pkgdir}"/etc/pam.d
   
   mkdir -pv "${pkgdir}"/{var,run}
   ln -snfv ../run "${pkgdir}"/var/run
   
   mkdir -pv "${pkgdir}"/usr/share/doc
   
   make BUILDROOT="${pkgdir}" install
   
   rm -rf "${pkgdir}"/tmp
   
   ln -svnf ../cups/doc-${pkgver} "${pkgdir}"/usr/share/doc/cups-${pkgver}
   echo "ServerName /var/run/cups/cups.sock" > "${pkgdir}"/etc/cups/client.conf
   
   cp ../../cups "${pkgdir}"/etc/pam.d/cups
   
   rm -rf "${pkgdir}"/var/run
}

