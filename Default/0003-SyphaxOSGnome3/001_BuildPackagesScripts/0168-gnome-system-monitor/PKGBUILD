# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=gnome-system-monitor
pkgver=3.24.0
pkgrel=2
pkgdesc="The GNOME System Monitor package contains GNOME's replacement for gtop."
arch=('x86_64')
url="http://ftp.gnome.org/pub/gnome/sources/gnome-system-monitor/3.24"
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c32b45062411aaef06681e5e28fd2c24ea8b362168bae83992944ec8e02063f4')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"

   #Fix compile issue under gnome 3.24.0 to link aginst .lo and not .o
   sed -i 's/gsm-resources.o/gsm-resources.lo/g' src/Makefile.am
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
      
   ./configure --prefix=/usr             \
               --enable-systemd          \
               --enable-broken-wnck      \
               --disable-static
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   make DESTDIR="${pkgdir}" install
}

