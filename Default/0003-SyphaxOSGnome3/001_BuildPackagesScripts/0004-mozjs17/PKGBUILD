# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=mozjs17
pkgver=17.0.0
pkgrel=4
pkgdesc="JS is Mozilla's JavaScript engine written in C/C++."
arch=('x86_64')
url="https://ftp.mozilla.org/pub/js"
license=('GPL')
groups=('desktop')
source=("$url/mozjs$pkgver.tar.gz")
md5sums=('20b6f8f1140ef6e47daa3b16965c9202')
depends=('rootfs')

prepare() {
   cd "$srcdir/mozjs$pkgver/js/src"
   
   sed -i 's/(defined\((@TEMPLATE_FILE)\))/\1/' config/milestone.pl
}

build() {
   cd "$srcdir/mozjs$pkgver/js/src"

   ./configure --prefix=/usr       \
               --enable-readline   \
               --enable-threadsafe \
               --with-system-ffi   \
               --with-system-nspr  \
               --disable-tests

   make  CXXFLAGS+='-fpermissive -w' 
}

package() {
   cd "$srcdir/mozjs$pkgver/js/src"

   make DESTDIR="$pkgdir" install
   
   find $pkgdir/usr/include/js-17.0/            \
        $pkgdir/usr/lib/libmozjs-17.0.a         \
        $pkgdir/usr/lib/pkgconfig/mozjs-17.0.pc \
        -type f -exec chmod -v 644 {} \;
}

