# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=NetworkManager
pkgver=1.10.8
pkgrel=2
pkgdesc="NetworkManager is a set of co-operative tools that make networking simple and straightforward."
arch=('x86_64')
url="http://ftp.gnome.org/pub/gnome/sources/NetworkManager/${pkgver:0:4}"
install=${pkgname}.install
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.xz"
        "${pkgname}.install")
sha256sums=('eb4ac8ce75fed5ec804f409caec7b54342d4e01512baf7d7fc119fd40ac8a938'
            '2de1347fa92bab517619f7a9c82de8258075eb2229fa150cce3fd9dda8215af4')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -i "/seems to be moved/s/^/#/" build-aux/ltmain.sh
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr                                   \
               --sysconfdir=/etc                               \
               --localstatedir=/var                            \
               --with-nmtui                                    \
               --disable-ppp                                   \
               --disable-json-validation                       \
               --disable-ovs                                   \
               --with-session-tracking=systemd                 \
               --with-systemdsystemunitdir=/lib/systemd/system \
               --docdir=/usr/share/doc/network-manager-${pkgver}
   
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   mkdir -pv "${pkgdir}"/{var,run}
   ln -snfv ../run "${pkgdir}"/var/run
   
   make DESTDIR="${pkgdir}" install
   
   rm -rf "${pkgdir}"/var/run
}

