# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=NetworkManager
pkgver=1.10.2
pkgrel=2
pkgdesc="NetworkManager is a set of co-operative tools that make networking simple and straightforward."
arch=('x86_64')
url="http://ftp.gnome.org/pub/gnome/sources/NetworkManager/1.10"
install=${pkgname}.install
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.xz"
        "${pkgname}.install")
sha256sums=('169c34f50770e3c96b431f7d2cff654455246f2e6ccd46eccfb4454d4595625b'
            '2de1347fa92bab517619f7a9c82de8258075eb2229fa150cce3fd9dda8215af4')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -i "/seems to be moved/s/^/#/" build-aux/ltmain.sh
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   CXXFLAGS="-O2 -fPIC"                                        \
   ./configure --prefix=/usr                                   \
               --sysconfdir=/etc                               \
               --localstatedir=/var                            \
               --with-nmtui                                    \
               --disable-ppp                                   \
               --disable-json-validation                       \
               --disable-ovs                                   \
               --with-session-tracking=systemd                 \
               --with-systemdsystemunitdir=/lib/systemd/system \
               --docdir=/usr/share/doc/network-manager-${pkgver}
   
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   mkdir -pv "${pkgdir}"/{var,run}
   ln -snfv ../run "${pkgdir}"/var/run
   
   make DESTDIR="${pkgdir}" install
   
   rm -rf "${pkgdir}"/var/run
}

