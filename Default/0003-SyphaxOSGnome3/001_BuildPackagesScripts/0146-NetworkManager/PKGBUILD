# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=NetworkManager
pkgver=1.7.2
pkgrel=1
pkgdesc="NetworkManager is a set of co-operative tools that make networking simple and straightforward."
arch=('x86_64')
url="http://ftp.gnome.org/pub/gnome/sources/NetworkManager/1.7"
install=${pkgname}.install
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.xz"
        "${pkgname}.install")
sha256sums=('0d45b8ba3d91f04e6ca1eb1c60553389db6d1b89aefa20f64934f24667d41c41'
            '2de1347fa92bab517619f7a9c82de8258075eb2229fa150cce3fd9dda8215af4')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -i "/seems to be moved/s/^/#/" build-aux/ltmain.sh
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   CXXFLAGS="-O2 -fPIC"                                        \
   ./configure --prefix=/usr                                   \
               --sysconfdir=/etc                               \
               --localstatedir=/var                            \
               --with-nmtui                                    \
               --disable-ppp                                   \
               --disable-json-validation                       \
               --with-session-tracking=systemd                 \
               --with-systemdsystemunitdir=/lib/systemd/system \
               --docdir=/usr/share/doc/network-manager-${pkgver}
   
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   mkdir -pv "${pkgdir}"/{var,run}
   ln -snfv ../run "${pkgdir}"/var/run
   
   make DESTDIR="${pkgdir}" install
   
   rm -rf "${pkgdir}"/var/run
}

