# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=mozjs52
pkgver=52.5.2
pkgrel=2
pkgdesc="JavaScript interpreter and libraries"
arch=('x86_64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey"
license=(MPL)
depends=('rootfs')
_repo=https://hg.mozilla.org/mozilla-unified
source=("$_repo/archive/FIREFOX_${pkgver//./_}esr_RELEASE.tar.bz2"
        mozjs52-copy-headers.patch
        mozjs52-disable-mozglue.patch
        mozjs52-fix-soname.patch
        mozjs52-include-configure-script.patch)
sha256sums=('SKIP'
            'c5b3a88e4d10ef149aba6fc48d431db54ff266323fa22a5d549028fd794423cc'
            '5a84f02521f37de873991dd360a4c4bfdbdd2fb4a218e11be73f9cbbf02050e8'
            '06389b8e30465bb8e6fab6144dadedffa30595f143a686524da175b5cf5e8a27'
            'd91a89acd88bfc747a255050757a0c17139bf5c3508c2e1c3c6bb2056786a344')

prepare() {
   cd mozilla-unified-FIREFOX_${pkgver//./_}esr_RELEASE

   # https://anonscm.debian.org/git/pkg-gnome/mozjs52.git/tree/debian/patches
   patch -Np1 -i ../mozjs52-fix-soname.patch
   patch -Np1 -i ../mozjs52-copy-headers.patch
   patch -Np1 -i ../mozjs52-disable-mozglue.patch
   patch -Np1 -i ../mozjs52-include-configure-script.patch
}

build() {
   unset CPPFLAGS
   CFLAGS+=' -fno-delete-null-pointer-checks -fno-strict-aliasing -fno-tree-vrp -Wno-type-limits -Wno-implicit-fallthrough -Wno-uninitialized'
   CXXFLAGS+=' -fno-delete-null-pointer-checks -fno-strict-aliasing -fno-tree-vrp -Wno-type-limits -Wno-implicit-fallthrough -Wno-uninitialized'
   export CC=gcc CXX=g++ PYTHON=/usr/bin/python2

   cd mozilla-unified-FIREFOX_${pkgver//./_}esr_RELEASE/js/src
   sh configure \
      --prefix=/usr \
      --disable-optimize \
      --disable-strip \
      --enable-pie \
      --enable-posix-nspr-emulation \
      --enable-readline \
      --enable-shared-js \
      --enable-tests \
      --with-intl-api \
      --with-system-zlib \
      --without-system-icu
   make
}

package() {
   cd mozilla-unified-FIREFOX_${pkgver//./_}esr_RELEASE/js/src

   make DESTDIR="$pkgdir" install
   rm "$pkgdir"/usr/lib/*.ajs
   find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -c a-x {} +
}

