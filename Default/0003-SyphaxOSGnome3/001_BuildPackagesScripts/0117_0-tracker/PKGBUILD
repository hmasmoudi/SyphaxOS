# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=libtracker-sparql
pkgver=2.0.2
pkgrel=2
pkgdesc="tracker (SPARQL library)"
url="https://ftp.gnome.org/pub/gnome/sources/tracker/2.0"
arch=(x86_64)
license=(GPL)
source=("$url/${pkgname:3:7}-${pkgver}.tar.xz")
sha256sums=('ece71a56c29151a76fc1b6e43c15dd1b657b37162dc948fa2487faf5ddb47fda')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname:3:7}-${pkgver}"

   sed -i "/seems to be moved/s/^/#/" build-aux/ltmain.sh
}

build() {
   cd "$srcdir/${pkgname:3:7}-${pkgver}"

   ./configure --prefix=/usr --disable-unit-tests

   sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
   
   make
}

package() {
   cd "$srcdir/${pkgname:3:7}-${pkgver}"

   make DESTDIR="$pkgdir" -C src/libtracker-common install-libtracker_commonLTLIBRARIES
   make DESTDIR="$pkgdir" -C src/libtracker-data install-libtracker_dataLTLIBRARIES
   make DESTDIR="$pkgdir" -C src/libtracker-sparql install
   make DESTDIR="$pkgdir" -C src/libtracker-sparql-backend install
}
