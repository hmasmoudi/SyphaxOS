# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=nspr
pkgver=4.16
pkgrel=2
pkgdesc="Netscape Portable Runtime (NSPR) provides a platform-neutral API for system level and libc like functions."
arch=('x86_64')
url="https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src"
license=('GPL')
groups=('desktop')
source=("$url/$pkgname-$pkgver.tar.gz")
sha256sums=('9b3102d97665504aeee73363c11a21c062ad67a2522242368b7f019f96a53cd1')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/$pkgname-$pkgver/$pkgname"
   sed -ri 's#^(RELEASE_BINS =).*#\1#' pr/src/misc/Makefile.in
   sed -i 's#$(LIBRARY) ##' config/rules.mk
}

build() {
   cd "$srcdir/$pkgname-$pkgver/$pkgname"

   ./configure --prefix=/usr    \
               --with-mozilla   \
               --with-pthreads  \
               --enable-64bit

   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver/$pkgname"

   make DESTDIR="$pkgdir" install
}

