# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=nspr
pkgver=4.18
pkgrel=2
pkgdesc="Netscape Portable Runtime (NSPR) provides a platform-neutral API for system level and libc like functions."
arch=('x86_64')
url="https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src"
license=('GPL')
groups=('desktop')
source=("$url/$pkgname-$pkgver.tar.gz")
sha256sums=('b89657c09bf88707d06ac238b8930d3ae08de68cb3edccfdc2e3dc97f9c8fb34')
depends=('rootfs')

prepare() {
   cd "$srcdir/$pkgname-$pkgver/$pkgname"
   sed -ri 's#^(RELEASE_BINS =).*#\1#' pr/src/misc/Makefile.in
   sed -i 's#$(LIBRARY) ##' config/rules.mk
}

build() {
   cd "$srcdir/$pkgname-$pkgver/$pkgname"

   ./configure --prefix=/usr     \
               --with-mozilla    \
               --with-pthreads   \
               --enable-optimize \
               --disable-debug   \
               --enable-64bit

   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver/$pkgname"

   make DESTDIR="$pkgdir" install
}

