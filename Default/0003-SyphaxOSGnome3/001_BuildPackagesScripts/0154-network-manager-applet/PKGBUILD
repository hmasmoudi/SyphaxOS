# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=network-manager-applet
pkgver=1.8.2
pkgrel=1
pkgdesc="The NetworkManager Applet provides a tool and a panel applet used to configure wired and wireless network connections through GUI."
arch=('x86_64')
url="http://ftp.gnome.org/pub/gnome/sources/network-manager-applet/1.8"
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.xz")
sha256sums=('3e57e9cf545a336da7035af1ba6171900ebc023ef19a594fe170da64af84c925')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   sed -i "/seems to be moved/s/^/#/" ltmain.sh
   
   sed -ie 's/KDE;GNOME;/KDE;/g' nm-applet.desktop.in
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr       \
               --sysconfdir=/etc   \
               --disable-static    \
               --without-team      \
               --without-selinux
   make
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"
      
   make DESTDIR="${pkgdir}" install
}

