# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=gnome-control-center
pkgver=3.28.0
pkgrel=2
pkgdesc="The GNOME Control Center package contains the GNOME settings manager."
arch=('x86_64')
url="https://ftp.gnome.org/pub/gnome/sources/gnome-control-center/3.28"
license=('GPL')
groups=('desktop')
source=("$url/${pkgname}-${pkgver}.tar.xz"
        "cc-info-overview-panel.patch"
        "SyphaxOSLogoWhite.svg")
sha256sums=('SKIP'
            'SKIP'
            '47990b67706071d17d9f94cf95cd8a02328ee8eb7bdbecd87b7981711d583cb2')
depends=('rootfs')

prepare() {
   cd "$srcdir/${pkgname}-${pkgver}"
   
   cp -f ../SyphaxOSLogoWhite.svg ./panels/info/GnomeLogoVerticalMedium.svg

   patch -p1 -i ../cc-info-overview-panel.patch
}

build() {
   cd "$srcdir/${pkgname}-${pkgver}"

   meson build --prefix=/usr
   ninja -C build
}

package() {
   cd "$srcdir/${pkgname}-${pkgver}"

   DESTDIR="${pkgdir}" ninja -C build install
   
   sed -i "/OnlyShowIn/d" "$pkgdir/usr/share/applications/gnome-control-center.desktop"
   
   install -d -o root -g 102 -m 700 "$pkgdir/usr/share/polkit-1/rules.d"
}

