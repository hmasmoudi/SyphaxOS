# Maintainer: Hatem Masmoudi <hatem.masmoudi@gmail.com>

pkgname=gtk2
pkgver=2.24.31
pkgrel=2
pkgdesc="The GTK+ 2 package contains libraries used for creating graphical user interfaces for applications."
arch=('x86_64')
url="http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24"
license=('GPL')
groups=('desktop')
source=("$url/gtk+-${pkgver}.tar.xz")
sha256sums=('68c1922732c7efc08df4656a5366dcc3afdc8791513400dac276009b40954658')
depends=('rootfs>=2.0.0')

prepare() {
   cd "$srcdir/gtk+-$pkgver"
   
   sed -e 's#l \(gtk-.*\).sgml#& -o \1#' \
       -i docs/{faq,tutorial}/Makefile.in

   sed -i "/seems to be moved/s/^/#/" ltmain.sh
}

build() {
   cd "$srcdir/gtk+-$pkgver"

   ./configure --prefix=/usr --sysconfdir=/etc
   
   make
}

package() {
   cd "$srcdir/gtk+-$pkgver"

   make DESTDIR="$pkgdir" install
   
   rm "$pkgdir/usr/bin/gtk-update-icon-cache"
}

